GMPLBLD3 ; SLC/MKB -- Bld PL Selection Lists cont ;03/27/13
 ;;2.0;Problem List;**28,260002**;Aug 25, 1994
 ;
 ; This routine invokes IA #3991
 ;
ASSIGN ; Assign list to clinic, users: Expects GMPLSLST
 N DIE,DA,DR,RET,MSG
 D FULL^VALM1 G:+$G(GMPLSLST)'>0 ASQ
 I '$$VALLIST^GMPLAPI6(.RET,+GMPLSLST) D  G ASQ
 . D BLD^DIALOG(1250000.102,,,"MSG")
 . D EN^DDIOL($C(7))
 . D EN^DDIOL(.MSG)
 . N DIR,DUOUT,DTOUT,DIRUT
 . S DIR(0)="E" D ^DIR
 . Q
 ;
 K MSG
 D BLD^DIALOG(1250000.103,,,"MSG")
 D EN^DDIOL(.MSG)
 D ASGCLIN(+GMPLSLST)
 D USERS("1") ; assign
ASQ S VALMBCK="R",VALMSG=$$MSG^GMPLX
 Q
 ;
ASGCLIN(GMPLSLST) ;
 N CLIN,RETURN,LST,%
 S %=$$GETLIST^GMPLAPI1(.LST,+GMPLSLST,0,1)
 S CLIN=$$CLINIC(,$P($G(LST("LST","CLINIC")),U,2))
 I +$G(CLIN)>0 S %=$$ADDLOC^GMPLAPI5(.RETURN,GMPLSLST,CLIN)
 Q
 ;
CLINIC(PREFIX,SUFIX) ;
 N X,Y
 S:$G(SUFIX)'="" DIC("B")=SUFIX
 S DIC("S")="I $P(^(0),U,3)=""C"""
 S DIC="^SC(",DIC(0)="AEQM",DIC("A")=$G(PREFIX)_$$EZBLD^DIALOG(1250000.104)
 D ^DIC
 Q Y
 ;
USERS(ADD) ; -- select user(s) to de-/assign list
 N DIR,DIC,DIE,DR,DA,X,Y,GMPLUSER,GMPLI,RETURN,PRMT,MSG,PARM
 Q:+$G(GMPLSLST)'>0  S GMPLUSER=""
 S DIC="^VA(200,",DIC(0)="EQM",DIC("A")=$$EZBLD^DIALOG(1250000.107)
 F  D READ Q:+Y'>0  S GMPLUSER=GMPLUSER_U_+Y,DIC("A")=$$EZBLD^DIALOG(1250000.108)
 I '$L(GMPLUSER) D  Q
 . D BLD^DIALOG(1250000.109,,,"MSG")
 . D EN^DDIOL(.MSG)
 S DIR(0)="YA",DIR("A")=$$EZBLD^DIALOG(1250000.110),DIR("B")="NO"
 S PARM(1)=$P(GMPLSLST,U,2)
 S PARM(2)=$L(GMPLUSER,U)-1
 D:ADD BLD^DIALOG(1250000.111,.PARM,,"DIR(""?"")")
 D:'ADD BLD^DIALOG(1250000.112,.PARM,,"DIR(""?"")")
 D ^DIR Q:'Y
USR ;
 N %
 K MSG
 D:ADD BLD^DIALOG(1250000.113,$P(GMPLSLST,U,2),,"MSG")
 D:'ADD BLD^DIALOG(1250000.114,$P(GMPLSLST,U,2),,"MSG")
 D EN^DDIOL(.MSG)
 F GMPLI=1:1:$L(GMPLUSER,U) S DA=$P(GMPLUSER,U,GMPLI) I DA D
 . S %=$S(ADD:$$ASSUSR^GMPLAPI6(.RETURN,+GMPLSLST,DA),1:$$REMUSR^GMPLAPI6(.RETURN,+GMPLSLST,DA))
 . D EN^DDIOL($$PROVNAME^GMPLEXT(DA),,"!?4")
 D EN^DDIOL($$EZBLD^DIALOG(1250000.115),"","!!")
 Q
 ;
READ ; prompt for username, respond
 D EN^DDIOL(DIC("A")) R X:DTIME I '$T!("^"[X) S Y=-1 Q
 I X="?" D  G READ
 . D:ADD BLD^DIALOG(1250000.116,,,"MSG")
 . D:'ADD BLD^DIALOG(1250000.117,,,"MSG")
 . D EN^DDIOL(.MSG)
 I X?1"??".E D  G READ
 . I X="??" D
 . . S DIC("S")="I $P($G(^(125)),U,2)="_+GMPLSLST
 . . D BLD^DIALOG(1250000.118,$P(GMPLSLST,U,2),,"MSG")
 . . D EN^DDIOL(.MSG)
 . S D="B",DZ="??" D DQ^DICQ K D,DZ,DIC("S")
 D ^DIC G:Y'>0 READ
 Q
 ;
DELETE ; Delete Selection List
 N DIR,DIK,DA,X,Y,VIEW,USER,GMPCOUNT,GMPQUIT,GMPLSLST,RETURN,MSG,%
 S GMPCOUNT=0,GMPLSLST=$$LIST^GMPLBLD2("") Q:GMPLSLST=0
 D BLD^DIALOG(1250000.119,,,"MSG")
 D EN^DDIOL(.MSG)
 S %=$$LSTUSED^GMPLAPI1(.RETURN,+GMPLSLST)
 S GMPCOUNT=RETURN
 I GMPCOUNT D  Q
 . K MSG
 . D BLD^DIALOG(1250000.120,GMPCOUNT,,"MSG")
 . D EN^DDIOL($C(7))
 . D EN^DDIOL(.MSG)
 D EN^DDIOL($$EZBLD^DIALOG(1250000.121))
DEL1 S DIR(0)="Y",DIR("B")="NO"
 D BLD^DIALOG(1250000.122,,,"DIR(""A"")")
 D BLD^DIALOG(1250000.123,,,"DIR(""?"")")
 W $C(7),! D ^DIR Q:'Y
 K MSG
 D BLD^DIALOG(1250000.124,$P(GMPLSLST,U,2),,"MSG")
 D EN^DDIOL(.MSG)
 K RETURN
 S %=$$DELLST^GMPLAPI1(.RETURN,+GMPLSLST)
 D EN^DDIOL(".",,"?0")
 D EN^DDIOL($$EZBLD^DIALOG(1250000.115))
 D EN^DDIOL("","","!")
 Q
 ;
MENU ; -- init variables and list array for GMPL LIST MENU list template
 ;    Expects GMPLSLST=selection list
 N RETURN,GRP,IND,IND1,MSG,%,PRB
 S IND=0,IND1=0
 D EN^DDIOL($$EZBLD^DIALOG(1250000.623,$P(GMPLSLST,U,2)),,"!!")
 S %=$$GETLIST^GMPLAPI1(.RETURN,+GMPLSLST,"")
 M ^TMP("GMPLMENU1",$J)=RETURN
 F  S IND=$O(^TMP("GMPLMENU1",$J,"GRP",IND)) Q:'IND  D
 . S GRP=^TMP("GMPLMENU1",$J,"GRP",IND)
 . S ^TMP("GMPLMENU",$J,IND,0)=$P(^TMP("GMPLMENU1",$J,GRP),U,3,4)
 . F  S IND1=$O(^TMP("GMPLMENU1",$J,"GRP",IND,IND1)) Q:'IND1  D
 . . S PRB=^TMP("GMPLMENU1",$J,"GRP",IND,IND1)
 . . S ^TMP("GMPLMENU",$J,IND,IND1)=$P(PRB,U,4)_U_$P(PRB,U,1,2)
 K ^TMP("GMPLMENU1",$J)
 I '$D(^TMP("GMPLMENU",$J)) D  H 2 S VALMBCK="Q",VALMQUIT=1 Q
 . D BLD^DIALOG(1250000.125,,,"MSG")
 . D EN^DDIOL(.MSG)
 D BUILD^GMPLMENU
 Q
