GMPLBLDF ; SLC/MKB -- Build Problem Selection List from IB Enc Form ;03/11/13
 ;;2.0;Problem List;**260002**;Aug 25, 1994
EN ; Start here.
 N MSG
 S X="IBDF18" X ^%ZOSF("TEST") I '$T D  Q
 . D BLD^DIALOG(1250000.135,,,"MSG")
 . D EN^DDIOL(.MSG)
EN0 S GMPLFORM=$$GETFORM^IBDF18 G:'GMPLFORM EXIT
 D BLD^DIALOG(1250000.126,,,"MSG")
 D EN^DDIOL(.MSG)
 S X=$$COPYFORM^IBDF18(+GMPLFORM,"GMPL"),GMPL(0)=X
 I 'X D  G EN0
 . D BLD^DIALOG(1250000.136,,,"MSG")
 . D EN^DDIOL(.MSG)
EN1 ; Create list to copy problems into
 S DIR(0)="FA^3:30"
 D BLD^DIALOG(1250000.145,,,"DIR(""A"")")
 N RETURN
 S %=$$GETLIST^GMPLAPI1(.RETURN,$P(GMPLFORM,U,2))
 M ^TMP("GMPLLIST",$J)=RETURN
 S:'RETURN DIR("B")=$P(GMPLFORM,U,2)
 D BLD^DIALOG(1250000.137,,,"DIR(""?"")")
 K MSG
 D BLD^DIALOG(1250000.138,,,"MSG")
 D EN^DDIOL(.MSG)
EN2 D ^DIR G:$D(DUOUT)!($D(DTOUT)) EXIT
 N ERR,CLINIC K ^TMP("GMPLLIST",$J)
 S ERR=$$NEWLST^GMPLAPI1(.RETURN,Y)
 I ERR=0,$P(RETURN(0),U,1)="LISTXST" D  G EN2
 . K MSG
 . D EN^DDIOL($C(7))
 . D BLD^DIALOG(1250000.139,,,"MSG")
 . D EN^DDIOL(.MSG)
 I RETURN'>0 D  G EXIT
 . K MSG
 . D EN^DDIOL($C(7))
 . D BLD^DIALOG(1250000.140,,,"MSG")
 . D EN^DDIOL(.MSG)
 S GMPLSLST=RETURN_U_Y
 S CLINIC=$$CLINIC^GMPLBLD3("   ")
 I CLINIC S %=$$ADDLOC^GMPLAPI5(.RETURN,+GMPLSLST,CLINIC)
EN3 ; Here we go ...
 N PARM
 K MSG
 S PARM(1)=$P(GMPLFORM,U,2)
 S PARM(2)=$P(GMPLSLST,U,2)
 D BLD^DIALOG(1250000.141,.PARM,,"MSG")
 D EN^DDIOL(.MSG)
 N RET,GSEQ,PSEG,NSEQ,GMPLI,GHDR,ITEM,SOURCE
 S (GSEQ,PSEQ,GMPLI)=0,GHDR="" S:'+GMPL(1) GHDR=$P(GMPL(1),U,2),GMPLI=1
 K ^TMP("GMPLIST",$J),^TMP("GMPGRP",$J)
 S GSEQ=GSEQ+1,GMPLGRP=$$NEWGRP(GMPLFORM,GHDR,GSEQ)
 F  S GMPLI=$O(GMPL(GMPLI)) Q:GMPLI'>0  D
 . S ITEM=$G(GMPL(GMPLI)) Q:'$L(ITEM)
 . I '+ITEM D  Q
 . . K SOURCE M SOURCE=^TMP("GMPGRP",$J)
 . . S %=$$SAVGRP^GMPLAPI1(.RET,+GMPLGRP,.SOURCE)
 . . K ^TMP("GMPGRP",$J)
 . . S GSEQ=GSEQ+1,PSEQ=0,GMPLGRP=$$NEWGRP(GMPLFORM,$P(ITEM,U,2),GSEQ)
 . S PSEQ=PSEQ+1,ITEM=PSEQ_U_ITEM
 . S NSEQ=$E("0000",1,4-$L(PSEQ))_PSEQ_"N"
 . S ^TMP("GMPGRP",$J,NSEQ)=ITEM D EN^DDIOL(".",,"?0")
 K SOURCE M SOURCE=^TMP("GMPGRP",$J)
 S %=$$SAVGRP^GMPLAPI1(.RET,+GMPLGRP,.SOURCE)
 K SOURCE M SOURCE=^TMP("GMPLIST",$J)
 S %=$$SAVLST^GMPLAPI1(.RET,+GMPLSLST,.SOURCE)
 K ^TMP("GMPLIST",$J),^TMP("GMPGRP",$J)
 D EN^DDIOL($$EZBLD^DIALOG(1250000.100),"","?0")
EXIT ; Clean-up
 K GMPL,GMPLSLST,GMPLGRP,GMPLI,GMPLFORM,GHDR,GSEQ,PSEQ,NSEQ,ITEM
 Q
 ;
NEWGRP(FORM,HDR,SEQ) ; Create new group entries in #125.1 and #125.11
 N RETURN,NAME,ANAME,GRP,ITEM,NSEQ
 S ANAME=$E($P(FORM,U,2),1,23-$L(SEQ))_$$EZBLD^DIALOG(1250000.146)_SEQ
 I $L(HDR) S NAME=$$UP^XLFSTR(HDR) E  S NAME=ANAME
 S %=$$NEWCAT^GMPLAPI1(.RETURN,NAME)
 I RETURN=0 S NAME=ANAME S %=$$NEWCAT^GMPLAPI1(.RETURN,NAME,1)
 S ITEM=SEQ_U_+RETURN_U_HDR_"^1",NSEQ=$E("0000",1,4-$L(SEQ))_SEQ_"N"
 S ^TMP("GMPLIST",$J,NSEQ)=ITEM
NGQ S GRP=$P(RETURN,U,1,2)
 Q GRP
 ;
