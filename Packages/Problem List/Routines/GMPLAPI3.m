GMPLAPI3 ; RGI/CBR -- Problem List API - NEW,UPDATE NOTES ; 3/21/13
 ;;2.0;Problem List;**260002**;Aug 24, 1994
NEWNOTE(RETURN,GMPIFN,GMPROV,NOTES) ; Creates New Note Entries for Problem
 ; GMPIFN  Pointer to Problem
 ; GMPROV  Current Provider
 ; GMPVAMC Facility
 ; NOTES   Array of notes
 N HDR,LAST,TOTAL,I,FAC,NIFN,DELETED,ICDACTV,GMPVAMC
 S RETURN=0
 I '$$PRBIEN^GMPLCHK(.RETURN,.GMPIFN) Q 0
 I '$$PROVIEN^GMPLCHK(.RETURN,.GMPROV) Q 0
 I '$D(NOTES) D ERRX^GMPLAPIE(.RETURN,"INVPARAM","NOTES") Q 0
 S GMPVAMC=$$GMPVAMC^GMPLAPI2
 S %=$$DELETED^GMPLAPI2(.DELETED,GMPIFN)
 I DELETED D ERRX^GMPLAPIE(.RETURN,"PRBDLTD"," ("_$$PROBTEXT^GMPLX(GMPIFN)_")") Q 0
 ; Code Set Versioning (CSV)
 S %=$$CODESTS^GMPLAPI2(.ICDACTV,GMPIFN,DT)
 I 'ICDACTV D ERRX^GMPLAPIE(.RETURN,"ICDINACT"," ("_$$PROBTEXT^GMPLX(GMPIFN)_")") Q 0
 I $D(NOTES)=1 S NOTES(1)=NOTES
 I '$$LOCK^GMPLDAL(GMPIFN,11,"RETURN") Q 0
 D NEWNOTE^GMPLDAL3(GMPIFN,GMPVAMC,GMPROV,.NOTES)
 D UNLOCK^GMPLDAL(GMPIFN,11)
 S RETURN=1
 Q RETURN
 ;
UPDNOTE(RETURN,GMPIFN,OLDNOTE,NEWNOTE,GMPROV) ;
 ; GMPIFN
 ; OLDNOTE
 ; NEWNOTE = Note IFN ^ Facility ^ Text
 ; GMPROV
 I '$$PRBIEN^GMPLCHK(.RETURN,.GMPIFN) Q 0
 I '$$PROVIEN^GMPLCHK(.RETURN,.GMPROV) Q 0
 S NEWNOTE=$G(NEWNOTE)
 S OLDNOTE=$G(OLDNOTE)
 Q:NEWNOTE=OLDNOTE 0
 N NIFN,FAC,TEXT,OLDTEXT
 S NIFN=+NEWNOTE
 S FAC=$P(NEWNOTE,U,2)
 S TEXT=$P(NEWNOTE,U,3)
 S OLDTEXT=$P(OLDNOTE,U,3)
 D:TEXT'="" UPDNOTE^GMPLDAL(GMPIFN,FAC,NIFN,TEXT,OLDTEXT,GMPROV,"RETURN")
 Q:TEXT=OLDTEXT 0
 D:TEXT="" DELNOTE^GMPLDAL(GMPIFN,FAC,NIFN,GMPROV,"RETURN")
 S RETURN=1
 Q RETURN
 ;
NOTES(RETURN,GMPIFN,GMPACT,GMPFMT) ; Return comments for a problem - MULTI-DIVISIONAL
 ; GMPACT - Active only
 K RETURN
 S RETURN=0
 I '$$PRBIEN^GMPLCHK(.RETURN,.GMPIFN) Q 0
 I '$$NUM^GMPLCHK(.RETURN,.GMPFMT,"GMPFMT",1,0,1,4) Q 0
 D NOTES^GMPLDAL3(.RETURN,GMPIFN,$G(GMPFMT,1),$G(GMPACT))
 Q 1
 ;
FULLNTE(RETURN,GMPIFN) ;Get active comments for a note
 K RETURN
 D NOTES^GMPLDAL3(.RETURN,GMPIFN,4,1)
 Q 1
