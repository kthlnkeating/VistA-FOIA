GMPLAPI3 ; RGI/CBR -- Problem List API - NEW,UPDATE NOTES ; 3/26/13
 ;;2.0;Problem List;**260002**;Aug 24, 1994
NEWNOTE(RETURN,GMPIFN,GMPROV,NOTES) ; Creates New Note Entries for Problem
 ; GMPIFN  Pointer to Problem
 ; GMPROV  Current Provider
 ; GMPVAMC Facility
 ; NOTES   Array of notes
 N HDR,LAST,TOTAL,I,FAC,NIFN,DELETED,ICDACTV,GMPVAMC,NOTEOK
 S RETURN=0
 I '$$PRBIEN^GMPLCHK(.RETURN,.GMPIFN) Q 0
 I '$$PROVIEN^GMPLCHK(.RETURN,.GMPROV) Q 0
 I '$D(NOTES) D ERRX^GMPLAPIE(.RETURN,"INVPARAM","NOTES") Q 0
 I $D(NOTES)=1 S NOTES(1)=NOTES
 S NOTEOK=0
 F I=0:0 S I=$O(NOTES(I)) Q:I'>0  D
 . S NOTEOK=1
 I 'NOTEOK D ERRX^GMPLAPIE(.RETURN,"INVPARAM","NOTES") Q 0
 S GMPVAMC=$$GMPVAMC^GMPLAPI2
 S %=$$DELETED^GMPLAPI2(.DELETED,GMPIFN)
 I DELETED D ERRX^GMPLAPIE(.RETURN,"PRBDLTD"," ("_$$PROBTEXT^GMPLX(GMPIFN)_")") Q 0
 ; Code Set Versioning (CSV)
 S %=$$CODESTS^GMPLAPI2(.ICDACTV,GMPIFN,DT)
 I 'ICDACTV D ERRX^GMPLAPIE(.RETURN,"ICDINACT"," ("_$$PROBTEXT^GMPLX(GMPIFN)_")") Q 0
 I '$$LOCK^GMPLDAL(GMPIFN,11,"RETURN") Q 0
 D NEWNOTE^GMPLDAL3(GMPIFN,GMPVAMC,GMPROV,.NOTES)
 D UNLOCK^GMPLDAL(GMPIFN,11)
 S RETURN=1
 Q RETURN
 ;
UPDNOTE(RETURN,GMPIFN,NEWNOTE,GMPROV) ;
 ; GMPIFN
 ; OLDNOTE
 ; NEWNOTE = Note IFN ^ Facility ^ Text
 ; GMPROV
 N NIFN,FAC,TEXT,OLDTEXT
 S RETURN=0
 I '$$PRBIEN^GMPLCHK(.RETURN,.GMPIFN) Q 0
 I '$$PROVIEN^GMPLCHK(.RETURN,.GMPROV) Q 0
 I $G(NEWNOTE)="" D ERRX^GMPLAPIE(.RETURN,"INVPARAM","NEWNOTE") Q 0
 S NIFN=$P(NEWNOTE,U)
 I '$$NUM^GMPLCHK(.RETURN,NIFN,"NEWNOTE",0,0,1) Q 0
 S FAC=$P(NEWNOTE,U,2)
 I '$$NUM^GMPLCHK(.RETURN,FAC,"NEWNOTE",0,0,1) Q 0
 S TEXT=$P(NEWNOTE,U,3)
 S OLDTEXT=$$NOTE^GMPLDAL(GMPIFN,FAC,NIFN)
 I OLDTEXT="" D ERRX^GMPLAPIE(.RETURN,"NOTENFND") Q 0
 I TEXT=OLDTEXT S RETURN=1 Q 1
 D:TEXT'="" UPDNOTE^GMPLDAL(GMPIFN,FAC,NIFN,TEXT,GMPROV)
 D:TEXT="" DELNOTE^GMPLDAL(GMPIFN,FAC,NIFN,GMPROV)
 S RETURN=1
 Q 1
 ;
NOTES(RETURN,GMPIFN,GMPACT,GMPFMT) ; Return comments for a problem - MULTI-DIVISIONAL
 ; GMPACT - Active only
 K RETURN
 S RETURN=0
 I '$$PRBIEN^GMPLCHK(.RETURN,.GMPIFN) Q 0
 I '$$BOOL^GMPLCHK(.RETURN,.GMPACT,"GMPACT") Q 0
 I '$$NUM^GMPLCHK(.RETURN,.GMPFMT,"GMPFMT",1,0,1,4) Q 0
 D NOTES^GMPLDAL3(.RETURN,GMPIFN,$G(GMPFMT,1),$G(GMPACT))
 Q 1
 ;
FULLNTE(RETURN,GMPIFN) ;Get active comments for a note
 K RETURN
 D NOTES^GMPLDAL3(.RETURN,GMPIFN,4,1)
 Q 1
