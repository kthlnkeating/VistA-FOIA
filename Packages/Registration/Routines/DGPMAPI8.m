DGPMAPI8 ;RGI/VSL - PATIENT MOVEMENT API; 1/10/2013
 ;;5.3;Registration;**260003**;
GETLASTM(RETURN,DFN,DGDT) ; Get last patient movement
 N %,DGDT1,LMVT,MVT,FLDS,ADM,RPM,PAT
 K RETURN S RETURN=0
 I '$D(DGDT) D NOW^%DTC S DGDT=%
 S DGDT1=9999999.999999-DGDT
 D GETLASTM^DGPMDAL1(.LMVT,DFN,DGDT1)
 Q:LMVT(1)'>0 1
 D GETPAT^DGPMDAL2(.PAT,DFN,"401.3")
 S RETURN("SERILL")=PAT(401.3,"I")_U_PAT(401.3,"E")
 D GETMVT^DGPMDAL1(.MVT,+LMVT(1),".04")
 S RETURN("MTYPE")=MVT(.04,"I")_U_MVT(.04,"E")
 S RETURN("MFN")=LMVT(1)
 S RETURN("TYPE")=LMVT(2)
 S RETURN("DATE")=LMVT(3)
 S RETURN("MASTYPE")=LMVT(4)
 S RETURN("WARD")=LMVT(5)
 S RETURN("ROOMBED")=LMVT(6)
 S RETURN("PRYMPHY")=LMVT(7)
 S RETURN("FTSPEC")=LMVT(8)
 S RETURN("ADMIFN")=LMVT(13)
 S RETURN("ADMDT")=LMVT(13,1)
 S RETURN("DISIFN")=LMVT(17)
 S RETURN("ATNDPHY")=LMVT(18)
 S RETURN("FDEXC")=LMVT(19,1)
 S RETURN=1
 Q 1
 ;
GETPAT(RETURN,DFN) ; Get patient
 N IND,NAME,FLDS,NAMES,PAT
 S FLDS=".01;.02;.03;.05;.06;.08;.14;.351;.361;.3611"
 S NAMES="NAME;SEX;DOB;MSTAT;RACE;RELIG;MEANST;DTHDT;ELIG;ESTAT"
 D GETPAT^DGPMDAL2(.PAT,DFN,FLDS)
 D BUILD(.RETURN,.PAT,FLDS,NAMES)
 S RETURN=1
 Q 1
 ;
GETMVT(RETURN,MFN) ; Get movement
 N IND,NAME,FLDS,NAMES,MVT
 S FLDS=".01;.02;.03;.04;.06;.07;.08;.09;.1;.17;.18"
 S NAMES="DATE;TTYPE;PATIENT;TYPE;WARD;BED;PRYMPHY;FTSPEC;SHDIAG;DISCH;MASTYPE"
 D GETMVT^DGPMDAL1(.MVT,MFN,FLDS)
 D BUILD(.RETURN,.MVT,FLDS,NAMES)
 S RETURN=1
 Q 1
 ;
GETADM(RETURN,AFN) ; Get admission
 N IND,NAME,FLDS,NAMES,MVT,RPHY,RPHYMVT,DIAG,PTF
 S FLDS=".01;.02;.03;.04;.06;.07;.1;.11;.12;.17;.18;41;.16"
 S NAMES="DATE;TTYPE;PATIENT;TYPE;WARD;ROOMBED;SHDIAG;ADMSCC;ADMREG;DISCH;MASTYPE;FDEXC;PTF"
 D GETMVT^DGPMDAL1(.MVT,AFN,FLDS)
 D BUILD(.RETURN,.MVT,FLDS,NAMES)
 D GETPTF^DGPMDAL1(.PTF,+RETURN("PTF"),"20;20.1")
 S RETURN("ADMSRC")=PTF(20,"I")_U_PTF(20,"E")
 S RETURN("ELIGIB")=PTF(20.1,"I")_U_PTF(20.1,"E")
 D GETPAT^DGPMDAL2(.PAT,+RETURN("PATIENT"),"401.3")
 S RETURN("SERILL")=PAT(401.3,"I")_U_PAT(401.3,"E")
 S FLDS=".08;.09;.19"
 S NAMES="PRYMPHY;FTSPEC;ATNDPHY"
 S RPHY=$$GETRPHY^DGPMDAL1(AFN)
 D GETMVT^DGPMDAL1(.RPHYMVT,RPHY,FLDS)
 D GETDIAG^DGPMDAL2(.DIAG,RPHY)
 M RETURN("DIAG")=DIAG(99)
 D BUILD(.RETURN,.RPHYMVT,FLDS,NAMES)
 S RETURN=1
 Q 1
 ;
GETTRA(RETURN,AFN) ; Get transfer
 N IND,NAME,FLDS,NAMES,MVT,RPHY,RPHYMVT,DIAG
 S FLDS=".01;.02;.03;.04;.06;.07;.13;.14;.18;"
 S NAMES="DATE;TTYPE;PATIENT;TYPE;WARD;ROOMBED;RABSDT;ADMIFN;MASTYPE;"
 D GETMVT^DGPMDAL1(.MVT,AFN,FLDS)
 D BUILD(.RETURN,.MVT,FLDS,NAMES)
 D GETPAT^DGPMDAL2(.PAT,+RETURN("PATIENT"),"401.3")
 S RETURN("SERILL")=PAT(401.3,"I")_U_PAT(401.3,"E")
 S FLDS=".08;.09;.19"
 S NAMES="PRYMPHY;FTSPEC;ATNDPHY"
 S RPHY=$$GETRPHY^DGPMDAL1(AFN)
 I $G(RPHY)>0 D
 . D GETMVT^DGPMDAL1(.RPHYMVT,RPHY,FLDS)
 . D GETDIAG^DGPMDAL2(.DIAG,RPHY)
 . M RETURN("DIAG")=DIAG(99)
 . D BUILD(.RETURN,.RPHYMVT,FLDS,NAMES)
 S RETURN=1
 Q 1
 ;
GETDIS(RETURN,AFN) ; Get discharge
 N IND,NAME,FLDS,NAMES,MVT,RPHY,RPHYMVT
 S FLDS=".01;.02;.03;.04;.05;.06;.07;.14;.18;"
 S NAMES="DATE;TTYPE;PATIENT;TYPE;FCTY;WARD;ROOMBED;ADMIFN;MASTYPE;"
 D GETMVT^DGPMDAL1(.MVT,AFN,FLDS)
 D BUILD(.RETURN,.MVT,FLDS,NAMES)
 S RETURN=1
 Q 1
 ;
GETMVTT(RETURN,IFN) ; Get movement type
 N IND,NAME,FLDS,NAMES,MVT,RPHY,RPHYMVT
 S FLDS=".01;.02;.03;.04;.05;"
 S NAMES="NAME;TTYPE;MAS;STAT;ASKSPEC"
 D GETMVTT^DGPMDAL2(.MVT,+IFN,FLDS)
 D BUILD(.RETURN,.MVT,FLDS,NAMES)
 S RETURN=1
 Q 1
 ;
GETMASMT(RETURN,IFN) ; Get MAS movement type
 N IND,NAME,FLDS,NAMES,MVT
 S FLDS=".01;.02;.05;.06;50.01;50.02;50.03;"
 S NAMES="NAME;TTYPE;ASKSPEC;ASKFTY;ABS;CFADM;ASIH"
 D GETMASMT^DGPMDAL2(.MVT,+IFN,FLDS)
 D BUILD(.RETURN,.MVT,FLDS,NAMES)
 S RETURN=1
 Q 1
 ;
BUILD(RETURN,REC,FLDS,NAMES) ; 
 N IND
 F IND=0:0 S IND=$O(REC(IND)) Q:IND=""  D
 . S NAME=$$FLDNAME^SDMUTL(FLDS,NAMES,IND)
 . S RETURN(NAME)=REC(IND,"I")_U_REC(IND,"E")
 Q
ISDOM(RETURN,DFN,DGDT) ; Is patient on dom?
 N VAINDT,VADMVT,DGDOM,DGDOM1
 S:$D(DGDT) VAINDT=DGDT
 D DOM^DGMTR
 S RETURN=$G(DGDOM)
 Q 1
