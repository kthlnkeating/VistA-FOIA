DGPMVF ;ALB/MRL/MIR - FACILITY TREATING SPECIALTY; 1/10/2013
 ;;5.3;Registration;**60,200,268**;Aug 13, 1993
 ;
SELALL(ADM,OADM,DFN,AFN) ;
 N RE,DIAG
 S ADM("FTSPEC")=$$SELFTS(ADM("DATE"),$G(OADM("FTSPEC")))
 S ADM("PRYMPHY")=$$SELPPROV(ADM("DATE"),$G(OADM("PRYMPHY")))
 S ADM("ATNDPHY")=$$SELAPROV(ADM("DATE"),$G(OADM("ATNDPHY")))
 S DIAG(1)=$G(ADM("SHDIAG")) M:$D(OADM("DIAG")) DIAG=OADM("DIAG")
 D EDITDIAG(.DIAG,ADM("DATE"))
 M ADM("DIAG")=DIAG
 Q
SELFTS(DGDT,DEFAULT) ; Select facility treating specialty
 N PAR
 S PAR("PROMPT")=$$UP^XLFSTR($$EZBLD^DIALOG(4070000.032)),PAR("ENTITY")=PAR("PROMPT")
 S PAR("RTN")="LSTFTS^DGPMVF",PAR("FLAG")="R"
 S PAR("HELP1")=4070000.033,PAR("HELP2")=4070000.034,PAR("HELP3")=4070000.035
 Q $$SELECT^DGPMUTL1(.PAR,,DEFAULT)
 ;
SELPPROV(DGDT,DEFAULT) ; Select primary physician
 N PAR S PAR("FLDS")="NAME;NAME;INITIAL;TITLE"
 S PAR("PROMPT")=$$UP^XLFSTR($$EZBLD^DIALOG(4070000.036)),PAR("ENTITY")=PAR("PROMPT")
 S PAR("RTN")="LSTPROV^DGPMVF",PAR("HELP1")=4070000.037,PAR("HELP2")=4070000.038
 Q $$SELECT^DGPMUTL1(.PAR,,DEFAULT)
 ;
SELAPROV(DGDT,DEFAULT) ; Select attending physician
 N PAR S PAR("FLDS")="NAME;NAME;INITIAL;TITLE",PAR("FLAG")="R"
 S PAR("PROMPT")=$$UP^XLFSTR($$EZBLD^DIALOG(4070000.039)),PAR("ENTITY")=PAR("PROMPT")
 S PAR("RTN")="LSTPROV^DGPMVF",PAR("HELP1")=4070000.04,PAR("HELP2")=4070000.041
 Q $$SELECT^DGPMUTL1(.PAR,,DEFAULT)
 ;
EDITDIAG(DIAG,AFN) ; Edit text
 N DIC,DWPK,I K ^UTILITY($J,"DIAG",AFN)
 D EN^DDIOL($$UP^XLFSTR($$EZBLD^DIALOG(4070000.042))_":")
 F I=0:0 S I=$O(DIAG(I)) Q:I=""  S ^UTILITY($J,"DIAG",AFN,I,0)=DIAG(I)
 K DIAG
 S DIC="^UTILITY($J,""DIAG"","_AFN_",",DWPK=1
 D EN^DIWE
 F I=0:0 S I=$O(^UTILITY($J,"DIAG",AFN,I)) Q:I=""  S DIAG(I)=^UTILITY($J,"DIAG",AFN,I,0)
 K ^UTILITY($J,"DIAG",AFN)
 Q
LSTPATS(RETURN,SEARCH,START,NUMBER) ; Lists patients
 S %=$$LSTPATS^DGPMAPI7(.RETURN,.SEARCH,.START,.NUMBER)
 Q
LSTFTS(RETURN,SEARCH,START,NUMBER) ; Lists facility treating specialties
 S %=$$LSTFTS^DGPMAPI7(.RETURN,.SEARCH,.START,.NUMBER,.DGDT)
 Q
LSTPROV(RETURN,SEARCH,START,NUMBER) ; Lists active providers
 S %=$$LSTPROV^DGPMAPI7(.RETURN,.SEARCH,.START,.NUMBER,.DGDT)
 Q
