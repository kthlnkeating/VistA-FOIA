DGPMV20 ;ALB/MIR - DISPLAY DATES FOR SELECTION ; 8/26/13
 ;;5.3;Registration;**40,260005**;Aug 13, 1993
 W !!,"CHOOSE FROM:" S I=0 F  S I=$O(^UTILITY("DGPMVN",$J,I)) Q:'I  D WR
 Q
WR N MVT M MVT=^UTILITY("DGPMVN",$J,I)
 S DGIFN=MVT("ID"),Y=+MVT("DATE") X ^DD("DD")
 W !,$J(I,2),">  ",Y I 'DGONE W ?27,$E($P(MVT("TYPE"),"^",2),1,20)
 I DGPMT=4!(DGPMT=5) S DGPMLD=$P($G(MVT("LDGRSN")),U,1)
 D @("W"_DGPMT) K DGIFN,DGX,DGPMLD Q
W1 W ?50,"TO:  ",$E($P(MVT("WARD"),"^",2),1,17) I MVT("ROOMBED") W " [",$E($P(MVT("ROOMBED"),"^",2),1,10),"]"
 I +MVT("MASTYPE")=9 W !?23,"FROM:  ",$P(MVT("FCTY"),"^",2)
 Q
W2 Q:"^25^26^"[("^"_+MVT("MASTYPE")_"^")
 I "^43^45^"[("^"_+MVT("MASTYPE")_"^") W ?50,"TO:  ",$E($P(MVT("FCTY"),"^",2),1,18) Q
 I "^1^2^3^"[("^"_+MVT("MASTYPE")_"^") W ?50,"RETURN:  " S Y=+MVT("RABSDT") Q:'Y  X ^DD("DD") W Y Q
 W ?50,"TO:  ",$E($P(MVT("WARD"),"^",2),1,17) I +MVT("ROOMBED") W " [",$E($P(MVT("ROOMBED"),"^",2),1,10),"]"
 Q
W3 I +MVT("MASTYPE")=10 W ?50,"TO:  ",$E($P(MVT("FCTY"),"^",1),1,18)
 Q
W4 S X="" I +MVT("MASTYPE")=5 S X=$P(MVT("WARD"),U,2)
 I +MVT("MASTYPE")=6 S X=$P(MVT("FCTY"),U,2)
 W ?55,"TO:  ",$E($P(X,"^",1),1,20)
 I DGPMLD]"" W !?7,"REASON:  ",$E($P(MVT("LDGRSN"),"^",2),1,20),?35,"COMMENTS:  ",$P(MVT("LDGCOMM"),"^",2)
 Q
W5 W:DGONE ?30 W:'DGONE !?7 W "DISPOSITION: ",$S($P(MVT("LDGRSN"),"^")="a":"ADMITTED",$P(MVT("LDGRSN"),"^")="d":"DISMISSED",1:"") Q
W6 W:DGONE ?30 W:'DGONE !?7 W "SPECIALTY:  ",$E($P(MVT("FTSPEC"),"^",2),1,18)
 W:DGONE !?7 W:'DGONE ?37 W "PROVIDER :  ",$E($P(MVT("PRYMPHY"),"^",2),1,15)
 W:DGONE !?7 W:'DGONE ?33 W "ATTENDING:  ",$E($P(MVT("ATNDPHY"),"^",2),1,15)
 S DGDX=$S($D(MVT("DIAG")):$E(MVT("DIAG",1),1,30),1:"") I DGDX]"" W:DGONE ?37 W:'DGONE !?7 W "DX:  ",DGDX
 K DGDX Q
ENEX ;CALLED FROM DGPMEX FOR EXTENDED BED CONTROL/EXTENDED PATIENT INQ
 N %,ADM,MVTS,MVT,DH
 S IOP="HOME" D ^%ZIS S DGFL=0 W @IOF,!!,"ADMISSION:"
 S %=$$GETADM^DGPMAPI8(.ADM,DGPMCA) M MVT=ADM
 S DGPMT=1,DGONE=0 D WEX
 S DGPMT=2 W !!,"TRANSFERS:"
 S %=$$LSTPTRAN^DGPMAPI7(.MVTS,DFN,DGPMCA)
 F I=0:0 S I=$O(MVTS(I)) Q:'I  M MVT=MVTS(I) D WEX Q:DGFL
 G Q:DGFL S DGONE=1
 W !!,"TREATING SPECIALTY CHANGES:"
 S DGPMT=6,%=$$LSTPFTS^DGPMAPI7(.MVTS,DFN,DGPMCA)
 F I=0:0 S I=$O(MVTS(I)) Q:'I  M MVT=MVTS(I) D WEX Q:DGFL
 I 'DGFL W !!,"DISCHARGE:" I +ADM("DISCH")>0 S %=$$GETMVT^DGPMAPI8(.DH,ADM("DISCH")),DGPMT=3,DGONE=0 M MVT=DH D WEX
Q K DIR,I,J,DGDIS,DGIFN,DGX,DUOUT,DTOUT Q
WEX N MT S %=$$GETMVTT^DGPMAPI8(.MT,MVT("TYPE")),Y=+MVT("DATE")
 X ^DD("DD") W !?5,Y W:'DGONE ?27,$S($P(MT("PNAME"),"^",2)]"":$P(MT("PNAME"),"^",2),1:$E($P(MT("NAME"),"^",1),1,20))
 D @("W"_DGPMT) I $S(DGPMT=1:0,DGPMT'=3:1,1:0),($Y>(IOSL-5)) S DIR(0)="E" D ^DIR S DGFL='Y S:$D(DTOUT) DGFL=2 I 'DGFL W @IOF
 Q
