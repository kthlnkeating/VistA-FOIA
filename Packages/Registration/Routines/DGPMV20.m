DGPMV20 ;ALB/MIR - DISPLAY DATES FOR SELECTION ; 3/6/2013
 ;;5.3;Registration;**40,260005**;Aug 13, 1993
 W !!,"CHOOSE FROM:" S I=0 F  S I=$O(MVTS(I)) Q:'I  D WR
 Q
WR S DGIFN=MVTS(I,"ID"),Y=+MVTS(I,"DATE") X ^DD("DD")
 W !,$J(I,2),">  ",Y I 'DGONE W ?27,$E($P(MVTS(I,"TYPE"),"^",2),1,20)
 I DGPMT=4!(DGPMT=5) S DGPMLD=$P($G(MVTS(I,"LDGRSN")),U,1)
 D @("W"_DGPMT) K DGIFN,DGX,DGPMLD Q
W1 W ?50,"TO:  ",$E($P(MVTS(I,"WARD"),"^",2),1,17) I MVTS(I,"ROOMBED") W " [",$E($P(MVTS(I,"ROOMBED"),"^",2),1,10),"]"
 I +MVTS(I,"MASTYPE")=9 W !?23,"FROM:  ",$P(MVTS(I,"FCTY"),"^",2)
 Q
W2 Q:"^25^26^"[("^"_+MVTS(I,"MASTYPE")_"^")
 I "^43^45^"[("^"_+MVTS(I,"FTSPEC")_"^") W ?50,"TO:  ",$E($P(MVTS(I,"FCTY"),"^",2),1,18) Q
 I "^1^2^3^"[("^"_+MVTS(I,"FTSPEC")_"^") W ?50,"RETURN:  " S Y=+MVTS(I,"RABSDT") X ^DD("DD") W Y Q
 W ?50,"TO:  ",$E($P(MVTS(I,"WARD"),"^",2),1,17) I +MVTS(I,"ROOMBED") W " [",$E($P(MVTS(I,"ROOMBED"),"^",2),1,10),"]"
 Q
W3 I +MVTS(I,"MASTYPE")=10 W ?50,"TO:  ",$E($P(MVTS(I,"FCTY"),"^",1),1,18)
 Q
W4 S X="" I +MVTS(I,"MASTYPE")=5 S X=$P(MVTS(I,"WARD"),U,2)
 I +MVTS(I,"MASTYPE")=6 S X=$P(MVTS(I,"FCTY"),U,2)
 W ?55,"TO:  ",$E($P(X,"^",1),1,20)
 I DGPMLD]"" W !?7,"REASON:  ",$E($P(MVTS(I,"LDGRSN"),"^",2),1,20),?35,"COMMENTS:  ",$P(MVTS(I,"LDGCOMM"),"^",2)
 Q
W5 W:DGONE ?30 W:'DGONE !?7 W "DISPOSITION: ",$S($P(MVTS(I,"LDGRSN"),"^")="a":"ADMITTED",$P(MVTS(I,"LDGRSN"),"^")="d":"DISMISSED",1:"") Q
W6 W:DGONE ?30 W:'DGONE !?7 W "SPECIALTY:  ",$E($P(MVTS(I,"FTSPEC"),"^",2),1,18)
 W:DGONE !?7 W:'DGONE ?37 W "PROVIDER :  ",$E($P(MVTS(I,"PRYMPHY"),"^",2),1,15)
 W:DGONE !?7 W:'DGONE ?33 W "ATTENDING:  ",$E($P(MVTS(I,"ATNDPHY"),"^",2),1,15)
 ;S DGDX=$S($D(^DGPM(+DGIFN,"DX",1,0)):$E(^(0),1,30),1:"") I DGDX]"" W:DGONE ?37 W:'DGONE !?7 W "DX:  ",DGDX
 K DGDX Q
ENEX ;CALLED FROM DGPMEX FOR EXTENDED BED CONTROL/EXTENDED PATIENT INQ
 S IOP="HOME" D ^%ZIS S DGFL=0 W @IOF,!!,"ADMISSION:"
 S %=$$GETADM^DGPMAPI8(.ADM,DGPMCA),I=1 M MVTS(I)=ADM
 S DGPMT=1,DGONE=0 D WEX
 S DGPMT=2 W !!,"TRANSFERS:"
 S %=$$LSTPTRAN^DGPMAPI7(.MVTS,DFN,DGPMCA)
 F I=0:0 S I=$O(MVTS(I)) Q:'I  D WEX Q:DGFL
 G Q:DGFL S DGONE=1
 W !!,"TREATING SPECIALTY CHANGES:"
 S DGPMT=6,%=$$LSTPFTS^DGPMAPI7(.MVTS,DFN,DGPMCA)
 F I=0:0 S I=$O(MVTS(I)) Q:'I  D WEX Q:DGFL
 I 'DGFL W !!,"DISCHARGE:" I +ADM("DISCH")>0 S %=$$GETMVT^DGPMAPI8(.DH,ADM("DISCH")),DGX=+DH("DATE"),DGPMT=3,DGONE=0 D WEX
Q K DIR,I,J,DGDIS,DGIFN,DGX,DUOUT,DTOUT Q
WEX S %=$$GETMVTT^DGPMAPI8(.MT,MVTS(I,"TYPE")),Y=+MVTS(I,"DATE")
 X ^DD("DD") W !?5,Y W:'DGONE ?27,$S($P(MT("PNAME"),"^",2)]"":$P(MT("PNAME"),"^",2),1:$E($P(MT("NAME"),"^",1),1,20))
 D @("W"_DGPMT) I $S(DGPMT=1:0,DGPMT'=3:1,1:0),($Y>(IOSL-5)) S DIR(0)="E" D ^DIR S DGFL='Y S:$D(DTOUT) DGFL=2 I 'DGFL W @IOF
 Q
