DGPMAPI2 ;RGI/VSL - TRANSFER PATIENT API; 1/10/2013
 ;;5.3;Registration;**260003**;
CHKTRA(RETURN,PARAM,TYPE) ; Check transfer parameters
 N TXT,TMP,ERR K RETURN S RETURN=0 ; patient
 I TYPE=23!(TYPE=25)!(TYPE=26) S RETURN=1 Q 1
 I $G(PARAM("ADMIFN"))="" S TXT(1)="PARAM('ADMIFN')" D ERRX^DGPMAPIE(.RETURN,"INVPARM",.TXT) Q 0
 D GETMVT^DGPMDAL1(.TMP,$G(PARAM("ADMIFN")))
 I TMP=0 S TXT(1)="PARAM('ADMIFN')" D ERRX^DGPMAPIE(.RETURN,"INVPARM",.TXT) Q 0
 K TMP ; admission date
 I $G(PARAM("DATE"))=""!(+$G(PARAM("DATE"))<1800000) S TXT(1)="PARAM('DATE')" D ERRX^DGPMAPIE(.RETURN,"INVPARM",.TXT) Q 0
 K TMP ; type of movement
 I $G(PARAM("TYPE"))="" S TXT(1)="PARAM('TYPE')" D ERRX^DGPMAPIE(.RETURN,"INVPARM",.TXT) Q 0
 D GETMVTT^DGPMDAL2(.TMP,$G(PARAM("TYPE")))
 I TMP=0 S TXT(1)="PARAM('TYPE')" D ERRX^DGPMAPIE(.RETURN,"INVPARM",.TXT) Q 0
 I $G(TMP(.02,"I"))'=2 S TXT(1)="PARAM('TYPE')" D ERRX^DGPMAPIE(.RETURN,"INVPARM",.TXT) Q 0
 K TMP ; ward
 I $G(PARAM("WARD"))="" S TXT(1)="PARAM('WARD')" D ERRX^DGPMAPIE(.RETURN,"INVPARM",.TXT) Q 0
 D GETWARD^DGPMDAL2(.TMP,$G(PARAM("WARD")),".01;400;200*")
 I TMP=0 S TXT(1)="PARAM('WARD')" D ERRX^DGPMAPIE(.RETURN,"INVPARM",.TXT) Q 0
 I TMP=1,TMP(400,"I")="" S TXT(1)="PARAM('WARD')" D ERRX^DGPMAPIE(.RETURN,"INVPARM",.TXT) Q 0
 I TMP=1,$D(TMP("OOS")),'$$ISWRDACT^DGPMAPI7(.TMP,PARAM("DATE")) S TXT(1)="PARAM('WARD')" D ERRX^DGPMAPIE(.RETURN,"WRDINACT",.TXT) Q 0
 K TMP ; roombed
 I $G(PARAM("ROOMBED"))="" S TXT(1)="PARAM('ROOMBED')" D ERRX^DGPMAPIE(.RETURN,"INVPARM",.TXT) Q 0
 D GETBED^DGPMDAL2(.TMP,$G(PARAM("ROOMBED")),".01;.2;100*;200*")
 I TMP=0 S TXT(1)="PARAM('ROOMBED')" D ERRX^DGPMAPIE(.RETURN,"INVPARM",.TXT) Q 0
 I '$D(TMP("W",PARAM("WARD"))) S TXT(1)="PARAM('ROOMBED')" D ERRX^DGPMAPIE(.RETURN,"INVPARM",.TXT) Q 0
 I $D(TMP("OOS")),'$$ISBEDACT^DGPMAPI7(.TMP,PARAM("DATE")) S TXT(1)="PARAM('ROOMBED')" D ERRX^DGPMAPIE(.RETURN,"BEDINACT",.TXT) Q 0
 I $$ISBEDOCC^DGPMAPI7(PARAM("ROOMBED"),PARAM("PATIENT")) S TXT(1)="PARAM('ROOMBED')" D ERRX^DGPMAPIE(.RETURN,"INVPARM",.TXT) Q 0
 K TMP ; facility treating specialty
 I $G(PARAM("FTSPEC"))="" S RETURN=1 Q 1
 D GETFTS^DGPMDAL2(.TMP,$G(PARAM("FTSPEC")),".01;100*")
 I TMP=0 S TXT(1)="PARAM('FTSPEC')" D ERRX^DGPMAPIE(.RETURN,"INVPARM",.TXT) Q 0
 I $D(TMP("E")),'$$ISFTSACT^DGPMAPI7(.TMP,PARAM("DATE")) S TXT(1)="PARAM('ADMREG')" D ERRX^DGPMAPIE(.RETURN,"INVPARM",.TXT) Q 0
 K TMP ;attender
 I $G(PARAM("ATNDPHY"))="" S TXT(1)="PARAM('ATNDPHY')" D ERRX^DGPMAPIE(.RETURN,"INVPARM",.TXT) Q 0
 I '$$SCREEN^DGPMDD($G(PARAM("ATNDPHY")),,+$G(PARAM("DATE"))) D  Q 0
 . S TXT(1)="PARAM(""ATNDPHY"")" D ERRX^DGPMAPIE(.RETURN,"INVPARM",.TXT)
 S ERR=0 ; primary physician
 I $G(PARAM("PRYMPHY"))'="" D  Q:ERR 0
 . I '$$SCREEN^DGPMDD($G(PARAM("PRYMPHY")),,+$G(PARAM("DATE"))) D  Q
 . . S ERR=1,TXT(1)="PARAM(""PRYMPHY"")" D ERRX^DGPMAPIE(.RETURN,"INVPARM",.TXT)
 S RETURN=1
 Q 1
 ;
TRANSF1(RETURN,PARAM,TYPE,MAS,LMVT) ; Transfer patient
 ;
 ;Input
 ;
 ;Required elements include:
 ;  PARAM("DATE")=date time (admission date)
 ;  PARAM("ADMIFN")=admission IFN
 ;  PARAM("PATIENT")=patient
 ;  PARAM("TYPE")=type of movement
 ;  PARAM("WARD")=ward
 ;  PARAM("FTSPEC")=facility treating specialty
 ;  PARAM("ATNDPHY")=attending physician
 ;
 ;Optional elements include:
 ;  PARAM("ROOMBED")=roombed
 ;  PARAM("DIAG",1)=diagnosis array
 ;  PARAM("PRYMPHY")=primary physician
 ;  PARAM("SERILL")=condition (SERIOUSLY ILL)
 N %,TFN,MVT6,DFN
 S %=$$CHKTRA(.RETURN,.PARAM,TYPE)
 I %=0 Q 0
 S DFN=PARAM("PATIENT")
 I TYPE=25!(TYPE=26) S PARAM("WARD")=+LMVT("WARD")
 S TFN=$$ADDTRA(.RETURN,.PARAM)
 I MAS(.05,"I"),$D(PARAM("FTSPEC")) D
 . S PARAM("RELIFN")=TFN
 . S %=$$ADDFTS^DGPMAPI6(.MVT6,.PARAM)
 D SETTEVT^DGPMDAL1(TFN,+$G(MVT6),"A")
 D MVTEVT^DGPMAPI1(DFN,2,TFN)
 Q 1
 ;
ADDTRA(RETURN,PARAM) ; Add patient transfer
 N %,DFN,IFN1,IFN2,IFN6,DT1,DT2,PM2,PM6,MVT2
 S DFN=PARAM("PATIENT"),IFN1=PARAM("ADMIFN")
 S DT2=PARAM("DATE"),TYPE=PARAM("TYPE")
 S PM2(.01)=DT2 ; transfer date
 D ADDMVMTX^DGPMDAL1(.MVT2,.PM2)
 S IFN2=+MVT2 K PM2
 S PM2(.02)=2  ; transaction
 S PM2(.04)=+TYPE  ; type of movement
 S PM2(.03)=+DFN  ; patient
 S:$D(PARAM("FCTY")) PM2(.05)=+PARAM("FCTY")  ; patient
 S:$D(PARAM("ASHSEQ")) PM2(.22)=+PARAM("ASHSEQ")  ; ASIH sequence
 D UPDMVT^DGPMDAL1(.MVT2,.PM2,IFN2) K PM2
 S PM2(100)=DUZ,PM2(101)=$$NOW^XLFDT()
 S PM2(102)=DUZ,PM2(103)=$$NOW^XLFDT()
 S:$D(PARAM("RABSDT")) PM2(.13)=PARAM("RABSDT")
 S PM2(.14)=IFN1
 D UPDMVT^DGPMDAL1(.MVT2,.PM2,IFN2) K PM2
 S:$D(PARAM("WARD")) PM2(.06)=+PARAM("WARD")  ; ward
 S:$D(PARAM("ROOMBED")) PM2(.07)=+$G(PARAM("ROOMBED"))  ; roombed
 D UPDMVT^DGPMDAL1(.MVT2,.PM2,IFN2)
 D UPDPAT^DGPMAPI1(,.PARAM,PARAM("PATIENT"))
 Q IFN2
 ;
TRANSF(RETURN,PARAM) ; Transfer patient
 N %,TT,MAS,TYPE,DFN
 S RETURN=0
 S DFN=PARAM("PATIENT")
 S %=$$GETLASTM^DGPMAPI8(.LMVT,DFN)
 I +PARAM("DATE")<+LMVT("ADMDT") D ERRX^DGPMAPIE(.RETURN,"TRANBADM") Q 0
 S %=$$LOCKMVT^DGPMDAL1(DFN)
 D INITEVT^DGPMDAL1
 D GETMVTT^DGPMDAL2(.TT,PARAM("TYPE"))
 S TYPE=TT(.03,"I")
 D GETMASMT^DGPMDAL2(.MAS,TYPE)
 I 1 D
 . I TYPE=13 S %=$$ASIH(.RETURN,.PARAM) Q
 . I TYPE=43 S %=$$ASIHOF(.RETURN,.PARAM) Q
 . I "^1^2^3^"[(U_+TYPE_U) S %=$$ABS(.RETURN,.PARAM,.LMVT) Q
 . S %=$$TRANSF1(.RETURN,.PARAM,TYPE,.MAS,.LMVT) Q
 S RETURN=%
 D ULOCKMVT^DGPMDAL1(DFN)
 Q 1
 ;
ABS(RETURN,PARAM,LMVT) ; Absence transfer
 N TFN,DIS,DMFN
 S PARAM("WARD")=$G(LMVT("WARD"))
 S PARAM("ADMIFN")=$G(LMVT("ADMIFN"))
 S TFN=$$ADDTRA(.RETURN,.PARAM)
 D SETTEVT^DGPMDAL1(TFN,,"A")
 D MVTEVT^DGPMAPI1(DFN,2,TFN)
 Q TFN
 ;
ASIHOF(RETURN,PARAM) ; ASIH (Other facility) transfer
 N %,TFN,DIS,DMFN
 S %=$$GETLASTM^DGPMAPI8(.LMVT,DFN)
 S PARAM("WARD")=$G(LMVT("WARD"))
 S TFN=$$ADDTRA(.RETURN,.PARAM)
 M DIS=PARAM
 S DIS("TYPE")=34 K DIS("FCTY")
 S %=$$ADDDIS^DGPMAPI3(.RETURN,.DIS,PARAM("PATIENT"))
 S DMFN=+RETURN
 D SETTEVT^DGPMDAL1(TFN,,"A")
 D SETDEVT^DGPMDAL1(DMFN,"A")
 D MVTEVT^DGPMAPI1(DFN,2,TFN)
 Q TFN
 ;
ASIH(RETURN,PARAM) ; ASIH transfer
 N %,ADM,TFN,AFN,NADM,TRA,DIS,FTS,FTSFN
 D SETAEVT^DGPMDAL1(PARAM("ADMIFN"),,"P")
 S TFN=$$ADDTRA(.RETURN,.PARAM)
 M NADM=PARAM
 S NADM("TYPE")=10 ; mvmt type
 S %=$$ADDADM^DGPMAPI1(,.NADM)
 S AFN=+%,TRA(.15)=AFN,TRA(.22)=1
 D UPDMVT^DGPMDAL1(,.TRA,TFN)
 K NADM
 S NADM(.21)=TFN ; asih transfer
 S NADM(.22)=2 ; asih sequence
 D UPDMVT^DGPMDAL1(,.NADM,AFN)
 M DIS=PARAM
 S DIS("TYPE")=34
 S %=$$ADDDIS^DGPMAPI3(.RETURN,.DIS,PARAM("PATIENT"))
 S DMFN=+RETURN
 M FTS=PARAM
 S FTS("ADMIFN")=AFN,FTS("RELIFN")=AFN
 S %=$$ADDFTS^DGPMAPI6(.RETURN,.FTS)
 S FTSFN=+RETURN
 D SETTEVT^DGPMDAL1(TFN,,"A")
 D SETDEVT^DGPMDAL1(DMFN,"A")
 D SETAEVT^DGPMDAL1(PARAM("ADMIFN"),,"A")
 D SETAEVT^DGPMDAL1(AFN,FTSFN,"A")
 D MVTEVT^DGPMAPI1(DFN,2,TFN)
 Q 1
 ;
UPDTRA(RETURN,PARAM,TFN) ; Update transfer
 N %,MVT,OLD,DFN,RPHY S RETURN=0
 S %=$$GETTRA^DGPMAPI8(.OLD,TFN)
 ;S %=$$CHKTRA(.RETURN,.PARAM)
 S DFN=$P(OLD("PATIENT"),U)
 Q:%=0 0
 S %=$$LOCKMVT^DGPMDAL1(DFN)
 I %=0 S RETURN=0 D ERRX^DGPMAPIE(.RETURN,"FILELOCK") Q 0
 S RPHY=$$GETRPHY^DGPMDAL1(TFN)
 D INITEVT^DGPMDAL1
 D SETTEVT^DGPMDAL1(TFN,RPHY,"P")
 S MVT(.04)=PARAM("TYPE")
 S:$D(PARAM("RABSDT")) MVT(.13)=PARAM("RABSDT")
 S:$D(PARAM("WARD")) MVT(.06)=PARAM("WARD")
 S:$D(PARAM("ROOMBED")) MVT(.07)=PARAM("ROOMBED")
 D UPDMVT^DGPMDAL1(.RETURN,.MVT,TFN)
 S:+RPHY>0 %=$$UPDFTS^DGPMAPI6(.RETURN,.PARAM,RPHY)
 S %=$$UPDPAT^DGPMAPI1(.RETURN,.PARAM,DFN)
 D SETTEVT^DGPMDAL1(TFN,RPHY,"A")
 D MVTEVT^DGPMAPI1(DFN,2,TFN)
 D ULOCKMVT^DGPMDAL1(DFN)
 S RETURN=1
 Q 1
 ;
CANDEL(RETURN,TFN) ; Can delete transfer?
 N TRA,PMVT,NMVT,FLDS,ADM,ASIH,TXT,MVTT,PTFCO
 S RETURN=0,FLDS=".02;.03;.04;.14;.15;.16;.17;.18"
 D GETMVT^DGPMDAL1(.TRA,TFN,FLDS)
 D GETMVT^DGPMDAL1(.ADM,TRA(.14,"I"),FLDS)
 D GETPMVT^DGPMDAL3(.PMVT,TRA(.03,"I"),TRA(.14,"I"),,TFN,FLDS)
 D GETNMVT^DGPMDAL3(.NMVT,TRA(.03,"I"),TRA(.14,"I"),,TFN,FLDS)
 I $G(PMVT(.18,"I"))=43,$G(NMVT(.18,"I"))=42 S RETURN=1 Q 1
 I $D(NMVT(.04,"I")),'$$CANFOL^DGPMDAL3(NMVT(.04,"I"),PMVT(.04,"I")) D ERRX^DGPMAPIE(.RETURN,"DELTITP") Q 0
 I "^13^44^"[("^"_TRA(.18,"I")_"^") D ERRX^DGPMAPIE(.RETURN,"DELTMDTA") Q 0
 I TRA(.18,"I")=14,$G(ADM(.17,"I"))>0 D ERRX^DGPMAPIE(.RETURN,"DELTCDWD") Q 0
 D GETMVT^DGPMDAL1(.ASIH,TRA(.15,"I"),FLDS)
 I ASIH D  Q:PTFCO=1 0
 . D GETPTFCO^DGPTDAL1(.PTFCO,ASIH(.16,"I"))
 . I PTFCO=1  D ERRX^DGPMAPIE(.RETURN,"DELTCDPC") Q
 I "^14^43^45^"[("^"_TRA(.18,"I")_"^"),("^13^14^43^44^45^"[("^"_NMVT(.18,"I")_"^")) D  Q 0
 . D GETMVTT^DGPMDAL2(.MVTT,NMVT(.04,"I"))
 . S TXT(1)=MVTT(.01,"E")
 . D ERRX^DGPMAPIE(.RETURN,"DELTMMRF",.TXT) Q
 S RETURN=1
 Q 1
 ;
DELTRA(RETURN,TFN) ; Delete transfer
 N %,TRA,I,RPHY
 S RETURN=0
 D INITEVT^DGPMDAL1
 S %=$$CANDEL^DGPMAPI2(.RETURN,TFN) Q:'RETURN 0
 D GETMVT^DGPMDAL1(.TRA,TFN,".03;.15;.16;.17;.21")
 S RPHY=$$GETRPHY^DGPMDAL1(TFN)
 D INITEVT^DGPMDAL1
 D SETTEVT^DGPMDAL1(TFN,RPHY,"P")
 D DELMVT^DGPMDAL1(RPHY)
 D DELMVT^DGPMDAL1(TFN)
 D SETTEVT^DGPMDAL1(TFN,RPHY,"A")
 D MVTEVT^DGPMAPI1(TRA(.03,"I"),2,TFN)
 S RETURN=1
 Q 1
 ;
