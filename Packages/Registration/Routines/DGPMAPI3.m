DGPMAPI3 ;RGI/VSL - DISCHARGE PATIENT API; 1/10/2013
 ;;5.3;Registration;**260003**;
CHKDSCH(RETURN,PARAM,TYPE,MAS) ; Check discharge patient
 S RETURN=0
 I +TYPE=46 I $$GETAMT^DGPMDAL3(45)="" S TXT(1)=MAS(.01,"E") D ERRX^DGPMAPIE(.RETURN,"DSCNMVTD",.TXT) Q 0
 S RETURN=1
 Q 1
DISCH(RETURN,PARAM) ; Discharge patient
 ;
 ;Input
 ;
 ;Required elements include:
 ;  PARAM("DATE")=date time (admission date)
 ;  PARAM("PATIENT")=patient
 ;  PARAM("TYPE")=type of movement
 ;  PARAM("ADMIFN")=admission ifn
 N PM,APM,DFN,IFN,MVT,WARD,ADMIFN,DATE,PAT,DGDT,IFNP,MAS,PTF,RPTF
 N TFCTY,TT,TXT
 S DFN=PARAM("PATIENT")
 D GETMVTT^DGPMDAL2(.TT,PARAM("TYPE"))
 S TYPE=TT(.03,"I")
 D GETMASMT^DGPMDAL2(.MAS,TYPE)
 S %=$$CHKDSCH(.RETURN,.PARAM,TYPE,.MAS)
 I %=0 Q 0
 S %=$$LOCKMVT^DGPMDAL1(DFN)
 I %=0 S RETURN=0 D ERRX^DGPMAPIE(.RETURN,"FILELOCK") Q 0
 D INITEVT^DGPMDAL1
 D SETAEVT^DGPMDAL1(PARAM("ADMIFN"),,"A")
 I +TYPE=46 S TFCTY=PARAM("FCTY") K PARAM("FCTY")
 I +TYPE=41 S %=$$DELASH(.PARAM,.MAS,TYPE)
 S IFN=$$ADDDIS(.RETURN,.PARAM,DFN,TYPE)
 I +TYPE=46!(+TYPE=41) S PARAM("FCTY")=$G(TFCTY),%=$$ASIH(.PARAM,.MAS,TYPE)
 D SETDEVT^DGPMDAL1(IFN,"A")
 D MVTEVT^DGPMAPI1(DFN,3,IFN)
 D ULOCKMVT^DGPMDAL1(DFN)
 Q 1
 ;
DELASH(PARAM,MAS,TYPE) ; Delete ASIH discharge
 N ASH,MVT,ADM,FLD
 S FLD=".01;.03;.06;.14;.15;.16;.17;.21;"
 D GETMVT^DGPMDAL1(.ASH,PARAM("ADMIFN"),FLD)
 D GETMVT^DGPMDAL1(.MVT,ASH(.21,"I"),FLD)
 D GETMVT^DGPMDAL1(.ADM,MVT(.14,"I"),FLD)
 I +TYPE=41 D
 . D DELMVT^DGPMDAL1(ADM(.17,"I"))
 S PTF(70)="@",PTF(71)="@",PTF(72)="@"
 D UPDPTF^DGPMDAL1(,.PTF,ADM(.16,"I"))
 Q %
 ;
ASIH(PARAM,MAS,TYPE) ; ASIH discharge
 N ASH,MVT,ADM,TRMAS,FLD,TRA
 S FLD=".01;.03;.06;.14;.15;.16;.17;.21;"
 D GETMVT^DGPMDAL1(.ASH,PARAM("ADMIFN"),FLD)
 D GETMVT^DGPMDAL1(.MVT,ASH(.21,"I"),FLD)
 S PARAM("ADMIFN")=MVT(.14,"I")
 S TRMAS=$S(+TYPE=46:45,+TYPE=41:14,1:0)
 S PARAM("TYPE")=$$GETAMT^DGPMDAL3(TRMAS)
 S PARAM("ASHSEQ")=2
 S %=$$ADDTRA^DGPMAPI2(.TRA,.PARAM)
 Q %
 ;
ADDDIS(RETURN,PARAM,DFN,TYPE) ;
 D GETMVT^DGPMDAL1(.MVT,PARAM("ADMIFN"),".01;.03;.06;.16")
 S ADMIFN=PARAM("ADMIFN")
 S DGDT=PARAM("DATE")
 S PM(.01)=DGDT ; discharge date
 D ADDMVMTX^DGPMDAL1(.RETURN,.PM)
 S IFN=+RETURN
 ;D SETDEVT^DGPMDAL1(IFN,ADMIFN,"P")
 K PM
 S PM(.14)=ADMIFN
 S PM(.02)=3  ; transaction
 S PM(.04)=PARAM("TYPE")  ; type of movement
 S PM(.03)=DFN  ; patient
 S PM(.05)=$G(PARAM("FCTY"))  ; transfer facility
 S:+$G(TYPE)=46!(+$G(TYPE)=41) PM(.22)=1  ; ASIH sequence
 S PM(100)=DUZ,PM(101)=$$NOW^XLFDT()
 S PM(102)=DUZ,PM(103)=$$NOW^XLFDT()
 D UPDMVT^DGPMDAL1(.RETURN,.PM,IFN)
 S IFNP=MVT(.16,"I")
 S PTF(70)=DGDT
 D GETMVT^DGPMDAL1(.MVT,IFN,".01;.18")
 D GETMASMT^DGPMDAL2(.MAS,MVT(.18,"I"),".01;.08")
 S PTF(72)=MAS(.08,"I")
 D UPDPTF^DGPMDAL1(.RPTF,.PTF,IFNP)
 S PAT(401.3)="@"
 S PAT(401.4)="@"
 D RESET^DGPMDDCN
 D UPDPAT^DGPMDAL1(,.PAT,DFN)
 Q IFN
 ;
UPDDSCH(RETURN,PARAM,DMFN) ; Update discharge
 S RETURN=0
 D SETDEVT^DGPMDAL1(DMFN,"P")
 S DIS(.01)=PARAM("DATE")
 S DIS(.04)=PARAM("TYPE")
 S DIS(.05)=$G(PARAM("FCTY"))
 D UPDMVT^DGPMDAL1(,.DIS,DMFN)
 D SETDEVT^DGPMDAL1(DMFN,"A")
 D MVTEVT^DGPMAPI1(PARAM("PATIENT"),3,DMFN)
 S RETURN=1
 Q 1
CANDEL(RETURN,DMFN) ; Can delete discharge?
 S RETURN=0
 S RETURN=1
 Q 1
DELDSCH(RETURN,DMFN) ; Delete discharge
 N DIS,DA
 S RETURN=0,DA=DMFN
 S %=$$CANDEL^DGPMAPI3(.RETURN,DMFN) Q:'RETURN 0
 D INITEVT^DGPMDAL1
 D GETMVT^DGPMDAL1(.DIS,DMFN,".03;.14;.16;.17;.21")
 D SETAEVT^DGPMDAL1(DIS(.14,"I"),,"P")
 D SETDEVT^DGPMDAL1(DMFN,"P")
 D DELMVT^DGPMDAL1(DMFN)
 D SETAEVT^DGPMDAL1(DIS(.14,"I"),,"A")
 D SETDEVT^DGPMDAL1(DMFN,"A")
 D MVTEVT^DGPMAPI1(DIS(.03,"I"),3,DMFN)
 S RETURN=1
 Q 1
