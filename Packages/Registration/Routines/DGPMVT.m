DGPMVT ;RGI/VSL - TRANSFER PATIENT; 1/10/2013
 ;;5.3;Registration;**260003**;Aug 13, 1993
 ;
TRANSF ;
 N DFN,TRAS
 S DFN=$$SELPAT^DGPMVN("LSTTPATS^DGPMVT",4070000.09) G:DFN=-1 TRANSF Q:'DFN
 D MEANST^DGPMVN(DFN)
 D PATSTAT^DGPMVN(DFN,1)
 S %=$$CONTINUE^DGPMVN()
 G:%="Q"!(%="") TRANSF
TRANSF1 ;
 N RE,ERR,TRA,LMVT,TFN,NEW
 S %=$$LSTPTRAN^DGPMAPI7(.TRA,DFN)
 S TFN=$$SELMVT^DGPMVN(.TRA,.NEW,4070000.091)
 S %=$$GETLASTM^DGPMAPI8(.LMVT,DFN,TFN)
 G:TFN'>0 TRANSF
 I NEW D NEWTRA(.RE,TFN,DFN,.LMVT) I 1
 E  D UPDTRA(.RE,TFN,.LMVT)
 I RE=0 D
 . I $P(RE(0),U)="FILELOCK" D BLD^DIALOG(4070000.083,,,"ERR") I 1
 . E  S ERR(1)=$P(RE(0),U,2)
 . D EN^DDIOL(.ERR)
 G TRANSF
 Q
 ;
UPDTRA(RETURN,TFN,LMVT) ;
 N OTRA,NTRA,DELETED
 S RETURN=1
 S %=$$GETTRA^DGPMAPI8(.OTRA,TFN)
UPD ;
 S %=$$SELDATE(OTRA("DATE")) Q:%="^"
 I %="@" S DELETED=$$DELTRA(TFN) I DELETED Q
 G:$D(DELETED) UPD
 S NTRA("DATE")=%
 S NTRA("ADMIFN")=+OTRA("ADMIFN")
 S NTRA("PATIENT")=+OTRA("PATIENT")
 D SELALL(.NTRA,.OTRA,OTRA("PATIENT"),TFN,.LMVT)
 S %=$$UPDTRA^DGPMAPI2(.RETURN,.NTRA,TFN)
 Q
 ;
DELTRA(TFN) ; Delete transfer
 N %,TXT,PARAM,DEFAULT,RETURN,ERR
 S TXT(1)="ADMISSION"
 S PARAM("PROMPT")=4070000.084,PARAM("HELPT")=$$EZBLD^DIALOG(4070000.085,.TXT)
 S DEFAULT=2
 S %=$$CANDEL^DGPMAPI2(.ERR,TFN)
 I ERR=0 D EN^DDIOL($P(ERR(0),U,2)) Q 0
 S %=$$YN^DGPMUTL1(.PARAM,.DEFAULT)
 Q:%'=1 0
 S %=$$DELTRA^DGPMAPI2(.RETURN,TFN)
 I RETURN=0 D EN^DDIOL($P(RETURN(0),U,2)) Q 0
 Q 1
 ;
SELDATE(DGDT) ;
 N PAR
 S PAR("PROMPT")=$$UP^XLFSTR($$EZBLD^DIALOG(4070000.113))
 S PAR("HELP1")=4070000.077,PAR("HELP2")=4070000.077
 S PAR("VALID")="VALDT^DGPMVT"
 Q $$SELTEXT^DGPMUTL1(.PAR,$P(DGDT,U,2))
 ;
VALDT(DGDT,VAL) ; Is selected date valid
 N %DT,Y,X
 S VAL=0,X=DGDT
 I DGDT="@"!(DGDT="^") S VAL=-1 Q
 S %DT="SRXE",%DT(0)="-NOW"
 D ^%DT I Y<0 Q
 S VAL=Y
 Q
SELABSDT(DGDT,TRDT) ;
 N PAR
 S PAR("PROMPT")=$$UP^XLFSTR($$EZBLD^DIALOG(4070000.107))
 S PAR("HELP1")=4070000.108,PAR("HELP2")=4070000.109
 S PAR("VALID")="VALABS^DGPMVT"
 Q $$SELTEXT^DGPMUTL1(.PAR,$P(DGDT,U,2))
 ;
VALABS(DGDT,VAL) ; Is absence date valid
 N %DT,Y,X,X1,X2
 S VAL=0,X=DGDT
 I DGDT="@"!(DGDT="^")!(DGDT="") S VAL=-1 Q
 S %DT="EX"
 D ^%DT I Y<0 Q
 I +TYPE=1 S X1=$P(+TRDT,".",1),X2=4 D C^%DTC I Y>X D EN^DDIOL($$EZBLD^DIALOG(4070000.11)) Q
 I +TYPE=2 S X1=$P(+TRDT,".",1),X2=5 D C^%DTC I Y<X D EN^DDIOL($$EZBLD^DIALOG(4070000.111)) Q
 I +TYPE=3 S X1=$P(+TRDT,".",1),X2=30 D C^%DTC I Y>X D EN^DDIOL($$EZBLD^DIALOG(4070000.112)) Q
 S VAL=Y
 Q
NEWTRA(RETURN,TFN,DFN,LMVT) ;
 N %,TRA,RE,DIAG,TXT
 S TXT(1)=$$FMTE^XLFDT(TFN)
 S %=$$ASK^DGPMVN(4070000.094,4070000.002,1,.TXT)
 I %'=1 G TRANSF1
 S TRA("ADMIFN")=+$G(LMVT("ADMIFN"))
 S TRA("DATE")=TFN
 S TRA("PATIENT")=DFN
 D SELALL(.TRA,,DFN,,.LMVT)
 S %=$$TRANSF^DGPMAPI2(.RETURN,.TRA)
 Q %
 ;
SELADM(ADM,DFN) ; Select admission data
 N RE,DIAG
 S ADM("ADMSRC")=$$SELSRC^DGPMVA("")
 S ADM("FDEXC")=$$FDEXC^DGPMVA("")
 S ADM("ADMREG")=$$SELADREG^DGPMVA("")
 S ADM("ADMSCC")=$$SCCOND^DGPMVA("")
 S ADM("SHDIAG")=$$DIAG^DGPMVA("")
 D SELALL^DGPMVF(.ADM,,.DFN)
 Q
 ;
SELALL(TRA,OTRA,DFN,TFN,LMVT) ;
 N RE,DIAG,TYPE,MT,TT,DEF
 S TRA("TYPE")=$$SELTYPE($G(OTRA("TYPE")),DFN,$G(OTRA("DATE"),TRA("DATE")),.TFN)
 D GETMVTT^DGPMAPI8(.TT,TRA("TYPE"))
 S TYPE=TT("MAS")
 S %=$$GETMASMT^DGPMAPI8(.MT,TYPE)
 I MT("ASKFTY") S TRA("FCTY")=$$SELFCTY^DGPMVT(4070000.115,$G(OTRA("FCTY"))) Q
 I +TYPE=25!(+TYPE=26) Q
 I "^1^2^3^"[(U_+TYPE_U) D  I 1
 . S DEF=$G(OTRA("RABSDT"))
 . I +TYPE=3 S X1=$P(+TRA("DATE"),".",1),X2=30 D C^%DTC S DEF=X_U_$$FMTE^XLFDT(X)
 . S TRA("RABSDT")=$$SELABSDT(DEF,TRA("DATE"))
 E  D
 . Q:+TYPE=23
 . S:+TYPE'=24&(+TYPE'=22) TRA("WARD")=$$SELWARD^DGPMVA($G(OTRA("WARD")))
 . S:+TYPE=24!(+TYPE=22) TRA("WARD")=+LMVT("WARD")
 . S TRA("ROOMBED")=$$SELBED^DGPMVA($G(TRA("WARD")),$G(OTRA("ROOMBED")))
 . I +TYPE=13 D SELADM(.TRA,DFN)
 D:+MT("ASKSPEC") FTS(.TRA,.OTRA,DFN,.TFN)
 S TRA("SERILL")=$$SERILL^DGPMVA($G(OTRA("SERILL"),"S^SERIOUSLY ILL"))
 Q
FTS(TRA,OTRA,DFN,TFN) ;
 I +$G(OTRA("FTSPEC"))'>0 S %=$$ASK^DGPMVN(4070000.092,4070000.093) Q:%'=1
 D SELALL^DGPMVF(.TRA,.OTRA,.DFN,.TFN)
 Q
SELTYPE(DEFAULT,DFN,DGDT,TFN) ; Select transfer types
 N PAR
 S PAR("PROMPT")=$$UP^XLFSTR($$EZBLD^DIALOG(4070000.1)),PAR("ENTITY")=PAR("PROMPT")
 S PAR("RTN")="LSTTRTYP^DGPMVT",PAR("FLDS")="ID;NAME;TYPE;STATUS",PAR("FLAG")="R"
 S PAR("HELP1")=4070000.019,PAR("HELP2")=4070000.02,PAR("HELP3")=4070000.021
 Q $$SELECT^DGPMUTL1(.PAR,,DEFAULT)
 ;
SELFCTY(PROMPT,DEFAULT) ; Select transfer facility
 N PAR
 S PAR("PROMPT")=$$UP^XLFSTR($$EZBLD^DIALOG(PROMPT)),PAR("ENTITY")=PAR("PROMPT")
 S PAR("RTN")="LSTFCTY^DGPMVT",PAR("FLDS")="NAME;STATE;TYPE",PAR("FLAG")="R"
 S PAR("HELP1")=4070000.104,PAR("HELP2")=4070000.106,PAR("HELP3")=4070000.105
 Q $$SELECT^DGPMUTL1(.PAR,,.DEFAULT)
 ;
LSTTPATS(RETURN,SEARCH,START,NUMBER) ; Lists patients
 S %=$$LSTTPATS^DGPMAPI7(.RETURN,.SEARCH,.START,.NUMBER)
 Q
LSTTRTYP(RETURN,SEARCH,START,NUMBER) ; Lists movement types
 S %=$$LSTTRTYP^DGPMAPI7(.RETURN,.SEARCH,.START,.NUMBER,DFN,.DGDT,.TFN)
 Q
LSTFCTY(RETURN,SEARCH,START,NUMBER) ; Lists movement facilities
 S %=$$LSTFCTY^DGPMAPI7(.RETURN,.SEARCH,.START,.NUMBER)
 Q
