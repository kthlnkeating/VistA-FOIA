DGPMV3 ;ALB/MIR - ENTER TRANSACTION INFORMATION; 3/6/2013
 ;;5.3;Registration;**34,54,62,95,692,715,260005**;Aug 13, 1993
 D NOW^%DTC S DGNOW=%,DGPMHY=DGPMY,DGPMOUT=0 G:'DGPMN DT S X=DGPMY
 ;-- provider change
 I DGPMT=6,$D(DGPMPC) S PAR("DGPMPC")=1
 D VAR G DR
DT D VAR G:DGPM1X DR S (DGPMY,Y)=DGPMHY X ^DD("DD") W !,DGPMUC," DATE: ",Y,"// " R X:DTIME G Q:'$T!(X["^") I X="" G DR
 S %DT="SRXE",%DT(0)="-NOW" I X["?"!(Y<0) D HELP^%DTC G DT
 I X="@" G OKD
 D ^%DT I Y<0 D HELP^%DTC G DT
 S OLDDATE=DGPMY
 K %DT S DGPMY=Y,PAR("DATE")=Y D CHK^DGPMV30:(X]"")&(DGPMY'=+OLDDATE) I $D(DGPME) S DGPMY=DGPMHY W !,DGPME K DGPME G DT
DR ;select input template for transaction type
 S DIE="^DGPM(" I "^1^4^6^"[("^"_DGPMT_"^"),DGPMN S DIE("NO^")=""
 ;S DGODSPT=$S('$D(^DGPM(DGPMCA,"ODS")):0,^("ODS"):1,1:0)
 S %=DGPMT,API=$S(%=1:"GETADM",%=2:"GETTRA",%=4:"GETADM",1:"GETMVT")
 S DGDA=$S($G(DGPMDA):DGPMDA,1:$G(MVTSD(DGPMY)))
 I 'DGPMN S RTN="S %=$$"_API_"^DGPMAPI8(.OLD,DGDA)" X RTN M PAR=OLD
 S PAR("DATE")=DGPMY,OLD("DATE")=DGPMY,PAR("PATIENT")=DFN
 S DA=$S(DGPMN=1:0,1:DGPMDA),DGX=DGPMY
 D @("^DGPMX"_DGPMT)
 ;Modified in patch dg*5.3*692 to include privacy indicator node "DIR"
 K DGZ
 D:DGPMT'=4 @("^DGPMV3"_DGPMT) Q:$G(DGQUIT)
 N RE,API,RTN
 I DGPMT'=4&(DGPMT'=5),(DGPMT'=3),(DGPMT'=6) D SI^DGPMV33
 I DGPMN D  Q:RE=0
 . S %=DGPMT,API=$S(%=1:"ADMIT",%=2:"TRANSF",%=3:"DISCH",%=4:"LDGIN",%=5:"LDGOUT",%=6:"FTS")
 . S RTN="S %=$$"_API_"^DGPMAPI"_DGPMT_"(.RE,.PAR)" X RTN
 . I RE=0 W !,$P(RE(0),U,2) Q
 I 'DGPMN D  Q:RE=0
 . S %=DGPMT,API=$S(%=1:"UPDADM",%=2:"UPDTRA",%=3:"UPDDSCH",%=4:"UPDLDGIN",%=5:"UPDLDGOU",%=6:"UPDFTSE")
 . S RTN="S %=$$"_API_"^DGPMAPI"_DGPMT_"(.RE,.PAR,DGDA)" X RTN
 . I RE=0 W !,$P(RE(0),U,2) Q
 I RE,DGPMT<4 D
 . K TXT
 . S TXT(1)=$$EZBLD^DIALOG(4070000.008)
 . S TXT(2)=$$EZBLD^DIALOG("4070000.0"_$S(DGPMN:2,1:1)_DGPMT)
 . S:'DGPMN TXT(3)=$$EZBLD^DIALOG(4070000.018)
 . D EN^DDIOL($$EZBLD^DIALOG("4070000.01",.TXT))
EVENTS ;
 ;D MVTEVT^DGPMAPI7(+DFN,DGPMT,+RE,1)
Q S:$D(DGPMBYP) DGPMBYP=DGPMDA
 K DGIDX,DGOWD,DGOTY ;variables set in DGPMGLC - G&L corrections
 K DGODS,DGODSPT ;ods variables
 K %DT,DA,DGER,DGNOW,DGOK,DGPM0,DGPM0ND,DGPM2,DGPMA,DGPMAB,DGPMABL,DGPMDA,DGPMER,DGPMHY,DGPMNI,DGPMOC,DGPMOS,DGPMOUT,DGPMP,DGPMPHY,DGPMPHY0,DGPMPTF,DGPMSP,DGPMTYP,DGPMTN,DGPMWD,DGT,DGSV,DGX,DGX1
 K DIC,DIE,DIK,DR,I,I1,J,K,X,X1,X2,Y,^UTILITY("DGPM",$J) Q
 ;
OKD ;
 K %DT D:DGPMT=6 PRIOR^DGPMV36
 S RTN="S %=$$CANDEL^DGPMAPI"_DGPMT_"(.RE,DGPMDA)" X RTN
 I RE=0 W !,$P(RE(0),U,2) G Q
 W !,"Are you sure you want to delete this movement" S %=2 D YN^DICN G Q:%<0,DT:%=2 I '% W !?5,"Answer yes to delete this ",DGPMUC," or no to continue" G OKD
 S %=DGPMT,API=$S(%=1:"DELADM",%=2:"DELTRA",%=3:"DELDSCH",%=4:"DELLDGIN",%=5:"DELLDGOU",%=6:"ADDFTS")
 S RTN="S %=$$"_API_"^DGPMAPI"_DGPMT_"(.RE,DGPMDA)" X RTN
 I RE=0 W !,$P(RE(0),U,2)
 I RE,DGPMT<4 D
 . K TXT
 . S TXT(1)=$$EZBLD^DIALOG(4070000.008)
 . S TXT(2)=$$EZBLD^DIALOG("4070000.01"_DGPMT)
 . S TXT(3)=$$EZBLD^DIALOG(4070000.009)
 . D EN^DDIOL($$EZBLD^DIALOG("4070000.01",.TXT))
 Q
VAR ;Set up variables
 ;Modified in patch dg*5.3*692 to include privacy indicator node "DIR"
 S DA=DGPMDA ;DGPMP=Before edit
 ;I DGPMT=6 S Y=DGPMDA D PRIOR^DGPMV36
 S %=$$GETPMVT^DGPMAPI8(.PMVT,.DGPMCA,.DGPMY,.DGPMDA)
 S %=$$GETNMVT^DGPMAPI8(.NMVT,.DGPMCA,.DGPMY,.DGPMDA)
 S DGPMABL=0 I NMVT S %=$$GETMASMT^DGPMAPI8(.MAS,NMVT("MASTYPE")) S DGPMABL=+MAS("ABS") ;is the next movement an absence?
 ;I DGPMT=6 S Y=DGPMDA D PRIOR^DGPMV36
 Q
NEW ;Entry point to add a new entry to ^DGPM
 D NEW^DGPMV301 ; continuation of routine DGPMV3 in DGPMV301
 Q
 ;
PRODAT(NODE) ;-- This function will add the ward and other data from the
 ; previous TS movement to the provider TS movement.
 ;
 N X,Y
 S Y=NODE,X=$O(^DGPM("ATS",DFN,DGPMCA,9999999.9999999-$P(NODE,U))) I X S X=$O(^(X,0)) I X S X=$O(^(X,0)) I X S X=^DGPM(X,0)
 S $P(Y,U,4)=$P(X,U,4),$P(Y,U,9)=$P(X,U,9)
 Q Y
 ;
