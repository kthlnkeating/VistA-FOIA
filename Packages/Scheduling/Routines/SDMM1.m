SDMM1 ;ALB/GRR - MULTIPLE BOOKINGS ; 11/12/2012 8:16am
 ;;5.3;Scheduling;**28,206,168,327,260003**;Aug 13, 1993
MAKE(DFN,SC,SD,TYP,STYP,SL,SRT,LVL,TXT,LAST) ; Make appointmemnt
 N ERR
 S %=$$CHKAPP^SDMAPI2(.ERR,SC,DFN,SD,SL,.LVL)
 I ERR=0 D
 . I $P(ERR(0),U)="APTSDOB"!($P(ERR(0),U)="APTOVBK") D
 . . Q:'$G(ERR(1))&($P(ERR(0),U)="APTOVBK")
 . . S $P(ERR(0),U,2)=$S($G(ERR(1)):"CLINIC DOES NOT MEET THEN!!",1:"NO OPEN SLOTS ON "_$$DT(SD)_" AT THAT TIME")
 . . S $P(ERR(0),U,3)=1 S:$P(ERR(0),U)="APTOVBK" SDERRFT=1
 . . S:$P(ERR(0),U)="APTSDOB"&$G(ERR(1)) SDERRFT=1
 . S:$P(ERR(0),U)="APTWHEN" $P(ERR(0),U,2)="CLINIC DOES NOT MEET ON "_$$DT(SD)
 . I $P(ERR(0),U,3)=1 W !,$P(ERR(0),U,2) Q
 . I $P(ERR(0),U,3)>1 D
 . . S TXT1="...OK"
 . . I $P(ERR(0),U)="APTPAHA" W !,"PATIENT ALREADY HAS APPOINTMENT ON "_$$DT(SD)_" AT THAT TIME" Q
 . . S:$P(ERR(0),U)="APTOVBK" $P(ERR(0),U,2)=$$FMTE^XLFDT(SD)_" WILL BE AN OVERBOOK, OK",TXT1=""
 . . S:$P(ERR(0),U)="APTEXOB" $P(ERR(0),U,2)=$P($P(ERR(0),U,2),","),TXT1=" FOR "_$$FMTE^XLFDT(SD)_", OK"
 . . S TXT(1)=$P(ERR(0),U,2)
 . . S OV=$$OVB^SDM1(.TXT,2) I OV=2 Q
 . . I OV=1 S LAST=$$MAKE(DFN,SC,SD,TYP,STYP,SL,.SRT,$P(ERR(0),U,3)-1,.TXT,1)
 Q:ERR=0 $G(LAST)
 W !,"APPOINTMENT MADE ON "_$$FMTE^XLFDT(SD)
 S %=$$MAKE^SDMAPI2(.ERR,DFN,SC,SD,TYP,STYP,.SL,.SRT,.TXT,.CIO,.LAB,.XRAY,.EKG,.RQXRAY,.CNSLTLNK,.LVL)
 Q 1
 ;
DT(SD) ;
 S TXT=$P("JAN^FEB^MAR^APR^MAY^JUN^JUL^AUG^SEP^OCT^NOV^DEC","^",$E(SD,4,5))_" "_$E(SD,6,7)
 Q TXT
