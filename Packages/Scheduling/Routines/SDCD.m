SDCD ;BSN/GRR - DISCHARGE PATIENT FROM CLINIC ;3/21/13
 ;;5.3;Scheduling;**41,148,260003**;AUG 13, 1993
15 D:'$D(DT) DT^SDUTL
 N ENRLS,SENS
 S SDAMERR=0
 I '$G(SDFN) S Y=$$SELPAT^SDMUI() S:Y'=-1 DFN=+Y G:Y=-1 QUIT
 S:$G(SDFN) DFN=+SDFN S DA(2)=+DFN
 I '$G(SDCLN) S %=$$GETPENRL^SDMAPI3(.ENRLS,DFN,,) G:$D(ENRLS)'>1 NOPE
 I '$G(SDCLN) S DIC="^DPT("_DA(2)_",""DE"",",DIC("S")="I $P(^(0),""^"",2)']""""",VAUTSTR="clinic",VAUTNI=2,VAUTVB="VAUTC" D FIRST^VAUTOMA K DIC("S") Q:Y<0
 I '$G(SDCLN) S %=$$GETPENRL^SDMAPI3(.ENRLS,DFN,,1) I $D(ENRLS)'>1 D  G QUIT:$G(SDFN),15
 . S SDAMERR=1
 . D:$D(ENRLS)'>1 EN^DDIOL($$EZBLD^DIALOG(480000.105),,"!!")
 I $G(SDCLN) D  G QUIT:SDAMERR>0
 . S %=$$GETPENRL^SDMAPI3(.SENS,DFN,SDCLN),TXT(1)=$G(SENS(SDCLN,"NAME"))
 . I '$D(SENS(SDCLN,"EN")) D EN^DDIOL($$EZBLD^DIALOG(480000.079,.TXT),,"!!") S SDAMERR=1
 . S VAUTC(1)=+SDCLN M ENRLS=SENS
 N IND
 I $G(VAUTC)=1 F I=0:0 S I=$O(ENRLS(I)) Q:'I  S VAUTC(I)=I
 F IND=0:0 S IND=$O(VAUTC(IND)) Q:IND=""  D
 . S TXT(1)=ENRLS(VAUTC(IND),"NAME") D EN^DDIOL($$EZBLD^DIALOG(480000.104,.TXT),,"!!")
 . N ENS M ENS(VAUTC(IND))=ENRLS(VAUTC(IND))
 . D DISCH(.ENS,DFN,VAUTC(IND))
 G 15:'$G(SDFN)
QUIT ;
 K CLINIC,DFN,SC,SDF,SDST,VAUTC,VAUTD,VAUTNI,VAUSTR,VAUTVB,DIC
 Q
DISCH(ENS,DFN,SC) ;
 N RE,TMP,SD,RSN
 S %=$$CHKDISCH^SDMAPI3(.RE,DFN,$$NOW^XLFDT(),VAUTC(IND))
 I 'RE D EN^DDIOL($P(RE(0),U,2),,"!!,?10"),EN^DDIOL($$EZBLD^DIALOG(480000.102),,"!") I 1
 E  D
 . S SD=ENS(SC,"EN",1,"DISCHARGE"),RSN=ENS(SC,"EN",1,"REASON")
 . S DLG=480000.103,%=$$COL(.ENS,SC,.SD,.RSN) S:'% DLG=480000.102 K SDAMERR
 . S:% %=$$DISCH^SDMAPI3(.RE,DFN,SD,SC,.RSN)
 . D EN^DDIOL($$EZBLD^DIALOG(DLG))
 D EN^DDIOL(" ")
 ;D PAUSE^VALM1
 Q
 ;
COL(ENS,SC,SD,RSN) ;
 N IND,ERR S ERR=0
 F IND=0:0 S IND=$O(ENS(SC,"EN",IND)) Q:'IND  D
 . I ENS(SC,"EN",IND,"DISCHARGE")']"" D
 . . K X,Y D EN^DDIOL(" ")
 . . S DIR("A")="DATE OF DISCHARGE: "
 . . S DIR(0)="DAO^"_($E(DT,1,3)-80)_":"_DT_":EX" D ^DIR
 . . S:Y>0 SD=Y
 . . S:'Y ERR=1 Q:'Y
 . . K X,Y
 . . S DIR("A")="REASON FOR DISCHARGE: "
 . . S:$G(ENS(SC,"EN",IND,"REASON"))'="" DIR("B")=ENS(SC,"EN",IND,"REASON")
 . . S DIR(0)="FAO^2:80:EX" D ^DIR
 . . S:Y'="^" RSN=Y
 K DIR
 Q 'ERR
 ;
NOPE D EN^DDIOL($$EZBLD^DIALOG(480000.106),,"!") G QUIT:$G(SDFN),15
