SDCAPI1 ;RGI/CBR - SCHEDULING CONSULT API; 1/21/2012
 ;;5.3;scheduling;**260003**;08/13/93
GETAPCNS(RETURN,DFN,STPCOD) ; Get active/pending consult requests
 N LST,FLDS,SVS,LST,TT,X1,X2,DFN1
 K RETURN S RETURN=0 S:$D(DFN) DFN1=+DFN
 S %=$$CHKPAT^SDMAPI3(.RETURN,+DFN) Q:'% 0
 S FLDS(.01)="DATE",FLDS(.02)="PATIENT",FLDS(.04)="CLINIC",FLDS(.1)="TEXT"
 S FLDS(8)="STATUS",FLDS(1)="SERVICE",FLDS(10)="SENDER",FLDS(13)="TYPE"
 I STPCOD="" Q 0
 D GETCONSR^SDCDAL1(.LST,DFN1,.FLDS)
 N A,CPRSTAT,DTENTR,DTLMT,NOS,NOSHOW
 K TMP S NOSHOW="no-show",X1=DT,X2=-365 D C^%DTC S DTLMT=X
 S A=":"
 F  S A=$O(LST(A),-1) Q:'+A  D
 . D GETRQSV^SDCDAL1(.SVS,STPCOD,+LST(A,1)) Q:'+SVS("DILIST",0)
 . S DTENTR=$P(LST(A,.01),U) I DTENTR<DTLMT Q
 . S CPRSTAT=$P(LST(A,8),U) Q:$S(CPRSTAT=5!(CPRSTAT=6)!(CPRSTAT=8)!(CPRSTAT=13):0,1:1)
 . S PTIEN=$P(LST(A,.02),U)
 . I CPRSTAT=8 S STOP=0 D  Q:STOP
 . . I $$CONSLNKD^SDCDAL1(A) S STOP=1 Q
 . . S NOS=$O(LST(A,40,":"),-1) I '+NOS S STOP=1 Q
 . . S X2=LST(A,40,NOS,.01),X1=DT D ^%DTC I X'=""&(X>180) S STOP=1 Q
 . . D SCHED(PTIEN,STPCOD,.SHOW) I 'SHOW S STOP=1 Q
 . . ;CPRSTAT 13 is a cancel
 . I CPRSTAT=13 S NOS=$O(LST(A,40,":"),-1) Q:'+NOS  D
 . . S NOS=$O(LST(A,40,NOS),-1) I '+NOS S STOP=1 Q
 . . S X2=$P(LST(A,40,NOS,.01),U),X1=DT D ^%DTC I X'=""&(X>180) S STOP=1 Q
 . . S COMMENT=$G(LST(A,40,NOS,5,1)) I COMMENT'[NOSHOW S STOP=1 Q
 . M TT(A)=LST(A) D BLDCONS(.TT,.RETURN,.FLDS)
 S RETURN=1
 Q 1
 ;
SCHED(PTIEN,STPCOD,SHOW) ;===CONSULT IS SCHEDULE NOW CHECK IF IT HAS APPOINTMENT BY STOP CODE.
 N APT,APTS,SD,STOP,CLNC,CLN,STOPCOD
 S %=$$CHKPAT^SDMAPI3(.RETURN,+PTIEN) Q:'% 0
 S %DT="ST",X="T-1" D ^%DT
 S SD=Y,SD(0)=1,STOP=0,APT="",SHOW=1
 D GETAPTS^SDMDAL2(.APTS,PTIEN,.SD)
 F  S APT=$O(APTS("APT",APT)) Q:'+APT!(STOP)  D
 . S CLNC=$P(APTS("APT",APT,"CLINIC"),U)
 . Q:CLNC=""
 . D GETCLN^SDMDAL1(.CLN,CLNC,1)
 . S STOPCOD=$G(CLN(8)) Q:STOPCOD=""
 . I STOPCOD'=STPCOD S SHOW=1 Q
 . D GETCAPT^SDMDAL4(.CAPT,PTIEN,APT)
 . I $D(CAPT) S SHOW=0,STOP=1
 Q
 ;
BLDCONS(CIN,COUT,FLDS) ; Build consult list
 N FLD,NO
 N IN S IN=0
 F  S IN=$O(CIN(IN)) Q:'IN  D
 . F FLD=0:0 S FLD=$O(FLDS(FLD)) Q:'FLD  S COUT(IN,FLDS(FLD))=CIN(IN,FLD)
 . F NO=0:0 S NO=$O(CIN(IN,40,NO)) Q:'NO  D
 . . S COUT(IN,"ACT",NO,"DATE")=CIN(IN,40,NO,.01)
 Q
 ;
CANCEL(RETURN,CONS,SC,SD,IFN,RMK,WHO,ADM,AUTO,CNDIE,CNDA) ; appt was cancelled then mark consult as edit/resubmit, add comment.
 N CAPT,CNS,CLN,BY,CONSULT,SDPATNT
 S:$D(CONS) CONSULT=CONS
 K RETURN S RETURN=0
 S %=$$CHKCLN^SDMAPI3(.RETURN,+SC) Q:'% 0
 D GETCAPT^SDMDAL1(.CAPT,+SC,SD,IFN,"I")
 I '$D(CONS) S CONSULT=$G(CAPT(688,"I"))
 Q:'+CONSULT 0
 S SDPATNT=$G(CAPT(.01,"I"))
 D GETCONS^SDCDAL1(.CNS,CONSULT,"IE")
 S CPRSSTAT=$G(CNS(8,"E")) I CPRSSTAT'="" Q:CPRSSTAT'="SCHEDULED"
 S SNDPRV=$G(CNS(10,"I"))
 S USER=$G(CNS(10,"E")),Y=SD
 D DD^%DT S APPT=$E(SD,4,5)_"/"_$E(SD,6,7)_"/"_$E(SD,2,3)_" @ "_$P(Y,"@",2)
 D GETCLN^SDMDAL1(.CLN,+SC,1,1)
 S BY=$S($D(WHO):$S(WHO["P":" by the Patient.",WHO["C":" by the Clinic.",1:"."),$D(ADM):" for administrative purposes.",1:", whole clinic.")
 S COMMENT(1)=$P(CLN(.01),U)_" Appt. on "_APPT_" was cancelled"_BY
 S:$D(RMK) COMMENT(2)="Remarks: "_RMK
 N SDERR S SDERR=$$STATUS^GMRCGUIS(CONSULT,6,3,SNDPRV,"","",.COMMENT)
 S CNDIE="^GMR(123,"_CONS_",40,",CNDA=+$G(COMMENT(0))
 S AUTO(+SC,SD,SDPATNT)=CONS
 N FLDS S FLDS(688)="@"
 D UPDCAPT^SDMDAL4(.FLDS,+SC,SD,IFN)
 S RETURN=1
 Q 1
 ;
AUTOREB(RETURN,SC,SD,LNK,CIFN) ; Auto rebook
 N FLDS,Y,TME,CLN,CNS,SNDPRV,CSCHDT,COMMENT,ER
 N FLDS S FLDS(688)=LNK
 K RETURN S RETURN=0
 S %=$$CHKCLN^SDMAPI3(.RETURN,+SC) Q:'% 0
 D UPDCAPT^SDMDAL4(.FLDS,+SC,+SD,CIFN)
 S Y=+SD D DD^%DT S TME=$P(Y,"@",2)
 D GETCLN^SDMDAL1(.CLN,+SC,1,1)
 S COMMENT(1)=$P(CLN(.01),U)_" Consult Appt. on "_$E(SD,4,5)_"/"_$E(SD,6,7)_"/"_$E(SD,2,3)_" @ "_TME_" (Auto Rebooked)."
 S %DT="ST",X="NOW" D ^%DT S CSCHDT=Y
 D GETCONS^SDCDAL1(.CNS,LNK,"IE")
 S SNDPRV=$G(CNS(10,"I"))
 D SCH^SDQQCN2(.ER,LNK,SNDPRV,CSCHDT,0,,.COMMENT) K COMMENT
 S RETURN=1
 Q 1
 ;
NOSHOW(RETURN,SC,SD,DFN,CONS,CN,AUTO,NSDIE,NSDA) ;
 ;Appt. was a NoShow, then mark Consult as Edit/Resubmit, add comment using silent call to notify user.
 ;Variables NSDIE and NSDA used in calling routine for NoShow letter printed comment in consult.
 N CNS,CLN,SRV,CPRSSTAT,SNDPRV,NOSHOW,CSPRT,USER,APPT,COMMENT
 K RETURN S RETURN=0
 S %=$$CHKPAT^SDMAPI3(.RETURN,+DFN) Q:'% 0
 S %=$$CHKCLN^SDMAPI3(.RETURN,+SC) Q:'% 0
 D GETCONS^SDCDAL1(.CNS,CONS,"IE")
 S CPRSSTAT=$G(CNS(8,"E")),SNDPRV=$G(CNS(10,"I"))
 S NOSHOW="no-show",AUTO(+SC,+SD,+DFN)=CONS
 I CPRSSTAT'="" Q:CPRSSTAT'="SCHEDULED" 0
 D GETRQSV1^SDCDAL1(.SRV,$G(CNS(1,"I")),"123.09","IE")
 S:SRV(123.09,"E")'="" CSPRT=$G(SRV(123.09,"E"))
 S USER=$G(CNS(10,"E")),Y=+SD
 D DD^%DT S APPT=$E(+SD,4,5)_"/"_$E(+SD,6,7)_"/"_$E(+SD,2,3)_" @ "_$P(Y,"@",2)
 D GETCLN^SDMDAL1(.CLN,+SC,1,1)
 S COMMENT(1)=$P(CLN(.01),U)_" Appt. on "_APPT_" was a "_NOSHOW_"." ;no-show is a key word used by a search do not change
 N SDERR S SDERR=$$STATUS^GMRCGUIS(CONS,6,3,SNDPRV,"","",.COMMENT)
 S NSDIE="^GMR(123,"_CONS_",40,",NSDA=+$G(COMMENT(0))
 N FLDS S FLDS(688)="@"
 D UPDCAPT^SDMDAL4(.FLDS,+SC,+SD,CN)
 I $D(CSPRT) D EN^GMRCP5(CONS,"C",CSPRT)
 S RETURN=1
 Q 1
 ;
EDITCS(RETURN,CONS,SD,RMK,CLNC) ;Mark consult as scheduled
 N CSCHDT,SNDPRV,TME,X,Y,COMMENT,ER,CNS
 S RETURN=0
 S %DT="ST",X="NOW" D ^%DT S CSCHDT=Y
 D GETCONS^SDCDAL1(.CNS,CONS,"IE")
 S SNDPRV=$G(CNS(10,"I")),Y=+SD
 D DD^%DT S TME=$P($P(Y,"@",2),":",1,2)
 S COMMENT(1)=$P(CLNC,U,2)_" Consult Appt. on "_$E(+SD,4,5)_"/"_$E(+SD,6,7)_"/"_$E(+SD,2,3)_" @ "_TME
 S COMMENT(2)=RMK
 D SCH^SDQQCN2(.ER,CONS,SNDPRV,CSCHDT,0,,.COMMENT) K COMMENT
 S RETURN=1
 Q 1
 ;
