FBFHLX2 ;DSS/LJF - VISTA FEE 5010 UPGRADE ;4/4/2011
 ;;3.5;FEE BASIS;**122**;JAN 30, 1995;Build 8
 ;;Per VHA Directive 10-93-142, this routine should not be modified.
 Q
 ;
TIME(FBTYPE,FBDTG,FBFTRACK) ;
 N FBFENFLD,FBFPARIX,FBFPROOT,FBFSTFLD,FBFINTER  ; set by $$INIT
 N FBFDA,FBFJOB,FBSTFLAG,FBFPURGE,FBFXTMP,FBF2RDTH
 S FBTYPE=$G(FBTYPE),FBDTG=+$G(FBDTG),ZTSK=+$G(ZTSK),FBFTRACK=1  I 'FBDTG Q
 K ZTDTH
 I '$$INIT^FBMON K FBFTRACK Q
 ;
 I FBTYPE="START" D  Q
 . I $$FACTIVE^FBMON(.FBFTRACK) D:'ZTSK INFORM^FBMON(1,FBFTRACK("START"),FBFTRACK("END"),FBFTRACK("STATE")) Q
 . ;
 . S FBFDA(FBFPROOT,FBFPARIX,FBFSTFLD)=FBDTG D FILE^DIE("","FBFDA") K FBFDA
 . S ^XTMP("FBFHLX",0)=$$FMADD^XLFDT(FBDTG,3)_U_FBDTG_U_" FB FPPS TRANSMIT active process status."
 . S ^XTMP("FBFHLX","IEN")=$H_"^0^INIT^"
 . S FBFTRACK=0
 ;
 I FBTYPE="END" D
 . K ^XTMP("FBFHLX")
 . S FBFDA(FBFPROOT,FBFPARIX,FBFENFLD)=FBDTG D FILE^DIE("","FBFDA") K FBFDA
 Q
 ;
SETITIME(FBSTRT,FBEND) ; Set specific times to FB TRANSMIT START & FB TRANSMIT END
 N FBFDA,FBFENFLD,FBFPARIX,FBFPROOT,FBFSTFLD,FBFINTER  ; set by $$INIT
 I '$$INIT^FBMON Q
 S FBSTRT=$G(FBSTRT,$$NOW^XLFDT),FBEND=$G(FBEND,$$NOW^XLFDT)
 I FBSTRT,FBEND
 I  I $$FMDIFF^XLFDT(FBSTRT,$$FMADD^XLFDT($$NOW^XLFDT,-4),1)<5,$$FMDIFF^XLFDT(FBSTRT,$$FMADD^XLFDT($$NOW^XLFDT,-4),1)>0  ;from now, up to 4 days ago
 I  I $$FMDIFF^XLFDT(FBEND,$$FMADD^XLFDT($$NOW^XLFDT,-4),1)<5,$$FMDIFF^XLFDT(FBEND,$$FMADD^XLFDT($$NOW^XLFDT,-4),1)>0
 E  Q
 S FBFDA(FBFPROOT,FBFPARIX,FBFSTFLD)=FBSTRT
 S FBFDA(FBFPROOT,FBFPARIX,FBFENFLD)=FBEND
 D FILE^DIE("","FBFDA") K FBFDA
 Q
 ;
 ;FBFHLX2
