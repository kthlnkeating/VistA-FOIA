FBPRICE1 ;AISC/DMK,WOIFO/SAB - GENERIC PRICER INTERFACE CON'T ;9/14/2009
 ;;3.5;FEE BASIS;**56,55,77,108**;JAN 30, 1995;Build 115
 ;;Per VHA Directive 2004-038, this routine should not be modified.
 ;
 N FBCDCNT,FBCNT,FBI,FBLNCNT,FBRT,FBTL,FBVAL,ICDVDT
 S ICDVDT=FBCSVDT ; date for file 80 and 80.1 identifier logic
 S FBCNT=0 ; count of codes
 ;
ICD ;ask Dx
 W !
 F I=1:1:25 D  Q:X=""!($D(DTOUT))!($D(DUOUT))
 . S DIR(0)="PO^80:EQMZ"
 . F  D ^DIR Q:X=""!($D(DTOUT))!($D(DUOUT))!(+Y'>0)  S FBVAL=+Y,FBRT=$$CHKICD9^FBCSV1(FBVAL,FBCSVDT) I FBRT]"" S FBDX(I)=FBVAL,FBCNT=FBCNT+1 Q
 . K DIR
 . Q:'$G(FBDX(I))
 . ; ask POA
 . S DIR(0)="P^161.94:EQM"
 . D ^DIR K DIR
 . Q:$D(DTOUT)!$D(DUOUT)
 . S FBPOA(I)=+Y
 I $D(DTOUT)!($D(DUOUT)) G END^FBPRICE
 I '$G(FBDX(1)) W !,*7,"Must enter at least a primary diagnosis.",! G ICD
 K DIR,I
 ;
ADMITDX ;ask admitting diagnosis
 W ! S DIR(0)="162.5,39",DIR("A")="Admitting Diagnosis"
 D ^DIR K DIR
 G END^FBPRICE:$D(DIRUT)
 S FBVAL=+Y
 S FBRT=$$CHKICD9^FBCSV1(FBVAL,FBCSVDT)
 I FBRT="" G ADMITDX
 S FBADMTDX=FBVAL
 S FBCNT=FBCNT+1
 ;
PROC ;ask procedure codes
 W !
 S DIR(0)="PO^80.1:EQM"
 F I=1:1:25 D  Q:X=""!($D(DUOUT))!($D(DTOUT))
 . F  D ^DIR Q:X=""!($D(DUOUT))!($D(DTOUT))!(+Y'>0)  S FBVAL=+Y,FBRT=$$CHKICD0^FBCSV1(FBVAL,FBCSVDT) I FBRT]"" S FBPRC(I)=FBVAL,FBCNT=FBCNT+1 Q
 I $D(DTOUT)!($D(DUOUT)) G END^FBPRICE
 K DIR,I
 ;
 W ! S DIR(0)="162.5,6.6",DIR("A")="Billed Charges"
 D ^DIR K DIR G END^FBPRICE:$D(DIRUT)
 S FBBILL=$FN(Y,"",2),FBBILL=$TR(FBBILL,".")
 S FBBILL=$E("000000000",$L(FBBILL)+1,9)_FBBILL
 ;
 S DIR(0)="162.5,6.6",DIR("A")="Amount Claimed"
 D ^DIR K DIR G END^FBPRICE:$D(DIRUT)
 S FBCLAIM=$FN(Y,"",2),FBCLAIM=$TR(FBCLAIM,".")
 S FBCLAIM=$E("000000000",$L(FBCLAIM)+1,9)_FBCLAIM
 ;
 S FBOBL="000000"
 ;
STRING ;set-up message text for pricer
 W ! D WAIT^DICD
 D ADDRESS^FBAAV01 Q:$G(VATERR)  K VAT
 S FBTL=(FBCNT-1)\13+2 ; total number of lines needed
 S FBFLAG=1 D NEWMSG^FBAAV01
 S FBRESUB=2 ; 2 identifies the message as generic pricer
 S FBLNCNT=0 ; init invoice line counter
 D NEWLN^FBAAV6
 S FBSTR=FBSTR_FBTL_FBLNAM_FBFI_FBMI_FBSEX_FBDOB_FBLOS
 S FBSTR=FBSTR_FBDISP_FBBILL_FBCLAIM_FBAUTH_FBPAYT_FBOBL_"Y"
 S FBSTR=FBSTR_FBVID_FBMED_$E(PAD,1,29)_FBTDT_FBSTABR_"  "
 D STORE^FBAAV01
 ;
 D NEWLN^FBAAV6
 S FBCDCNT=1 ; count of codes in the line (=1 for admit dx)
 S FBSTR=FBSTR_$$DX^FBAAV6(FBADMTDX,FBCSVDT,"")
 ; loop thru Dx
 F FBI=1:1:25 Q:'$G(FBDX(FBI))  D
 . S FBCDCNT=FBCDCNT+1
 . I FBCDCNT=14 D
 . . D STORE^FBAAV01
 . . D NEWLN^FBAAV6
 . . S FBCDCNT=1
 . S FBSTR=FBSTR_$$DX^FBAAV6(FBDX(FBI),FBCSVDT,FBPOA(FBI))
 ; loop thru proc
 F FBI=1:1:25 Q:'$G(FBPRC(FBI))  D
 . S FBCDCNT=FBCDCNT+1
 . I FBCDCNT=14 D
 . . D STORE^FBAAV01
 . . D NEWLN^FBAAV6
 . . S FBCDCNT=1
 . S FBSTR=FBSTR_$$PROC^FBAAV6(FBPRC(FBI),FBCSVDT)
 ; pad remainder of last line with spaces and save it
 S FBSTR=$$LJ^XLFSTR(FBSTR,131," ")
 D STORE^FBAAV01
 ;
 D XMIT^FBAAV01 K FBFLAG
 W !,"Case sent to pricer.",!
 Q
