GMTSMHAP ; SLC/WAT - PRINT/EXTRACT FOR HRMH APPOINTMENT COMPONENT INFO ;11/03/11  06:57
 ;;2.7;Health Summary;**99**;Oct 20, 1995;Build 45
 ;
 ;EXTERNAL CALLS
 ;REMINDER LOCATION LIST ^PXRMD(810.9  5599
 ;^SC("AST"   4482
 ;$$SDAPI^SDAMA301  4433
 ;$$FMTE^XLFDT  10103
 ;
 ;
 Q
 ;
EN ;MAIN
 K ^TMP($J,"GMTS CLIN LIST"),^TMP($J,"GMTS APPT")
 N CLINCNT S CLINCNT=1
 N RMLL,RMLLSTP,RMCLINIC,RMCLNCNT
 N GMTSARR,SDCOUNT,SDDFN,SDDATE,SDAPPT,SDDATIME,SDCLINIC,CLINAME
 N TAB,LINE,IDX1,ERROR
 S RMLL=$O(^PXRMD(810.9,"B","VA-MH NO SHOW APPT CLINICS LL",""))
 S RMCLNCNT=0,IDX1=0,RMCLINIC=""
 S TAB="   ",LINE=0,ERROR=0
 I $G(RMLL)="" S SDCOUNT=0 D ERR Q  ;err and quit if RMLL not found
 F  S IDX1=$O(^PXRMD(810.9,RMLL,40.7,IDX1)) Q:IDX1'>0!(ERROR=1)  D
 .S RMLLSTP=^PXRMD(810.9,RMLL,40.7,IDX1,0)
 .S RMLLSTP=$P($G(RMLLSTP),"^") ;->this is the stop code, now get clinics for this stop code
 .Q:$D(^SC("AST",RMLLSTP))=0
 .F  S RMCLINIC=$O(^SC("AST",RMLLSTP,RMCLINIC)) Q:RMCLINIC=""!(ERROR=1)  D
 ..S ^TMP($J,"GMTS CLIN LIST",RMCLINIC)=RMCLINIC
 ;NOW HAVE LIST OF CLINICS TO SEARCH FOR APPOINTMENTS
 ;CALL SDAPI ONCE FOR ALL CLINICS IN THE LIST
 S GMTSARR(1)=DT ;date filter, can be FROM DATE;TO DATE; DT will get ALL from Today Forward
 ;GMTSARR(2)= is the clinic IEN - it will be an array reference in this case
 S GMTSARR(2)="^TMP($J,""GMTS CLIN LIST"""
 S GMTSARR(3)="R" ;appt status R=scheduled/kept, I=inpatient
 S GMTSARR(4)=DFN
 ;GMTSARR(13) ;PRIMARY STOP CODE
 S GMTSARR("FLDS")="1;2;4;3"
 S GMTSARR("SORT")="P" ;implement sort to order appointments by appointment date
 S SDCOUNT=$$SDAPI^SDAMA301(.GMTSARR)
 I SDCOUNT<0 S ERROR=1
 I SDCOUNT>0 D
 . ;get patient
 . S SDDFN=0 F  S SDDFN=$O(^TMP($J,"SDAMA301",SDDFN)) Q:SDDFN=""  D
 .. ;get appointment date/time
 .. S SDDATE=0 F  S SDDATE=$O(^TMP($J,"SDAMA301",SDDFN,SDDATE)) Q:SDDATE=""  D
 ... S SDAPPT=$G(^TMP($J,"SDAMA301",SDDFN,SDDATE)) ;appointment data
 ... S SDDATIME=$P($G(SDAPPT),"^",1) ;appointment date/time
 ... S CLINAME=$P($G(SDAPPT),"^",2),CLINAME=$P(CLINAME,";",2) ;CLINIC NAME
 ... S ^TMP($J,"GMTS APPT",LINE)=$$FMTE^XLFDT(SDDATIME,"5ZP")_TAB_$G(CLINAME),LINE=LINE+1
 I ERROR>0 D ERR Q
 I SDCOUNT'=0 K ^TMP($J,"SDAMA301")
 D:$D(^TMP($J,"GMTS APPT",0))>0 PRINT
 Q
 ;
PRINT ;print
 N LINE,IDXERR S LINE="",IDXERR=""
 F  S LINE=$O(^TMP($J,"GMTS APPT",LINE)) D  Q:LINE=""
 .Q:LINE=""
 .D CKP^GMTSUP Q:$D(GMTSQIT)
 .W:LINE=0 ?2,^TMP($J,"GMTS APPT",LINE)
 .W:LINE>0 !,?2,^TMP($J,"GMTS APPT",LINE)
 W !
ERR ;errors returned from SDAPI
 I $G(RMLL)="" D CKP^GMTSUP Q:$D(GMTSQIT)  W !,?2,"Reminder location list not found. Unable to retrun appointment data" Q
 F  S IDXERR=$O(^TMP($J,"SDAMA301",IDXERR)) Q:IDXERR'>0  D
 .D CKP^GMTSUP Q:$D(GMTSQIT)
 .W !,^TMP($J,"SDAMA301",IDXERR)
 Q
