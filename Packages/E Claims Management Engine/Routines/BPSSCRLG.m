BPSSCRLG ;BHAM ISC/SS - ECME LOGINFO ;05-APR-05
 ;;1.0;E CLAIMS MGMT ENGINE;**1,5,7,8,10,11**;JUN 2004;Build 27
 ;;Per VHA Directive 2004-038, this routine should not be modified.
 ;
 Q
 ;
EN ; -- main entry point for BPS LSTMN LOG
 D EN^VALM("BPS LSTMN LOG")
 Q 
 ;
HDR ; -- header code
 S VALMHDR(1)="Claim Log information"
 S VALMHDR(2)=""
 Q
 ;
INIT ; -- init variables and list array
 N BPSELCLM,LINE
 S BPSELCLM=$G(@VALMAR@("SELLN"))
 ;  piece 2: patient ien #2
 ;  piece 3: insurance ien #36
 ;  piece 4: ptr to #9002313.59
 S LINE=1
 S VALMCNT=$$PREPINFO(.LINE,$P(BPSELCLM,U,2),$P(BPSELCLM,U,3),$P(BPSELCLM,U,4))
 S:VALMCNT>1 VALMCNT=VALMCNT-1
 Q
 ;
HELP ; -- help code
 S X="?" D DISP^XQORM1 W !!
 K X
 Q
 ;
EXIT ; -- exit code
 Q
 ;
EXPND ; -- expand code
 Q
 ;
 ;
LOG ;entry point for LOG menu option
 N BPRET,BPSEL
 I '$D(@(VALMAR)) Q
 D FULL^VALM1
 W !,"Enter the line number for which you wish to print claim logs."
 S BPSEL=$$ASKLINE^BPSSCRU4("Select item","C","Please select SINGLE Rx Line.")
 I BPSEL<1 S VALMBCK="R" Q
 D SAVESEL(BPSEL,VALMAR)
 D EN
 S VALMBCK="R"
 Q
 ;
 ;save for ListManager
 ;BPSEL - selected line
 ;BPVALMR - parent VALMAR 
SAVESEL(BPSEL,BPVALMR) ;
 D CLEANIT
 S ^TMP("BPSLOG",$J,"VALM","SELLN")=BPSEL
 S ^TMP("BPSLOG",$J,"VALM","PARENT")=BPVALMR
 M ^TMP("BPSLOG",$J,"VALM","VIEWPARAMS")=@BPVALMR@("VIEWPARAMS")
 Q
 ;
CLEANIT ;
 K ^TMP("BPSLOG",$J,"VALM")
 Q
 ;input:
 ; BPDFN: patient ien #2
 ; BP36: insurance ien #36
 ; BP59: ptr to #9002313.59
 ; returns # of lines
PREPINFO(BPLN,BPDFN,BP36,BP59) ;
 N BPSECME
 I '$G(BP59) Q 0
 I '$G(BP36) Q 0
 I '$G(BPDFN) Q 0
 N BPSCRLNS S BPSCRLNS=17 ;(see "BPS LSTMN LOG" LM template: Bottom=21, Top = 4, 21-4=17)
 N BPX,BPRXIEN,BPRXN,BPREF,BP1,BPLSTCLM,BPLSTRSP,BPDAT59,BPUSR,BPSTRT,BPHIST,BPQ
 N BPDT,BPLN0,BPCNT,DFN,VADM
 S DFN=BPDFN D DEM^VADPT
 S BP1=$$RXREF^BPSSCRU2(BP59)
 S BPRXIEN=$P(BP1,U,1)
 S BPRXN=$$RXNUM^BPSSCRU2(+BPRXIEN)
 S BPREF=$P(BP1,U,2)
 S BPDAT59(0)=$G(^BPST(BP59,0))
 ;create history
 D MKHIST^BPSSCRU5(BP59,.BPHIST)
 ;
 S BPLN0=BPLN
 D SETLINE(.BPLN,"Pharmacy ECME Log")
 D SETLINE(.BPLN,"")
 S BPX=$$RJ^BPSSCR02("Rx #: ",20)_BPRXN_"/"_BPREF
 S BPSECME=$$ECMENUM^BPSSCRU2(BP59)
 S BPX=BPX_$$RJ^BPSSCR02("ECME #: ",20)_BPSECME
 D SETLINE(.BPLN,BPX)
 S BPX=$$RJ^BPSSCR02("Drug: ",20)_$$DRGNAM^BPSSCRU2($$GETDRG59^BPSSCRU2(BP59))
 D SETLINE(.BPLN,BPX)
 S BPX=$$RJ^BPSSCR02("Patient: ",20)
 S BPX=BPX_$$LJ^BPSSCR02($$PATNAME^BPSSCRU2(BPDFN)_" "_$$SSN4^BPSSCRU2(BPDFN),25)
 S BPX=BPX_$$LJ^BPSSCR02("Sex: "_$P($G(VADM(5)),"^",1),10)
 S BPX=BPX_$$LJ^BPSSCR02("DOB: "_$P($G(VADM(3)),"^",2)_"("_$G(VADM(4))_")",20)
 D SETLINE(.BPLN,BPX)
 S BPX=$$RJ^BPSSCR02("Transaction Number: ",20)
 S BPX=BPX_$P($G(^BPST(BP59,0)),U,1)
 D SETLINE(.BPLN,BPX)
 S BPX=$$RJ^BPSSCR02("Last Submitted: ",20)
 S BPSTRT=$P(BPDAT59(0),U,11) ;@# need to check with analyst if this is a START DATE
 I BPSTRT]"" S BPX=BPX_$$DATETIME^BPSSCRU5(BPSTRT)
 D SETLINE(.BPLN,BPX)
 S BPX=$$RJ^BPSSCR02("Last Submitted By: ",20)
 S BPUSR=$P(BPDAT59(0),U,10)
 I BPUSR]"" S BPX=BPX_$$GETUSRNM^BPSSCRU1(BPUSR)
 D SETLINE(.BPLN,BPX)
 ;
 ;latest claim
 S BP1=+$O(BPHIST("C",99999999),-1)
 I BP1=0 D SETLINE(.BPLN,""),SETLINE(.BPLN,"------ No electronic claims ------") Q BPLN
 S BP1=+$O(BPHIST("C",BP1,0))
 S BPX=$$RJ^BPSSCR02("Last VA Claim #: ",20)_$P($G(^BPSC(+BP1,0)),U,1)
 D SETLINE(.BPLN,BPX)
 F BPCNT=BPLN:1:BPLN0+BPSCRLNS D SETLINE(.BPLN,"")
 ;process history
 N BPTYPE,BPIEN,BPIENRS
 S BPDT=99999999
 F  S BPDT=$O(BPHIST("C",BPDT),-1) Q:+BPDT=0  D
 . S BPIEN=+$O(BPHIST("C",BPDT,0)) Q:BPIEN=""
 . D DISPCLM(.BPLN,BP59,BPIEN,+BPHIST("C",BPDT,BPIEN),$P(BPHIST("C",BPDT,BPIEN),U,2),BPDT)
 . S BPIENRS=0
 . F  S BPIENRS=$O(BPHIST("C",BPDT,BPIEN,"R",BPIENRS)) Q:+BPIENRS=0  D
 . . D DISPRSP(.BPLN,BP59,BPIENRS,+BPHIST("C",BPDT,BPIEN,"R",BPIENRS),$P(BPHIST("C",BPDT,BPIEN,"R",BPIENRS),U,2),BPDT)
 . . D DISPPYR(.BPLN,BPIENRS)
 Q BPLN
 ;
 ;increments BPLINE
SETLINE(BPLINE,BPSTR) ;
 D SET^VALM10(BPLINE,BPSTR)
 S BPLINE=BPLINE+1
 Q
 ;display claim record
DISPCLM(BPLN,BP59,BPIEN02,BP57,BPSTYPE,BPSDTALT) ;
 N BPSCRLNS S BPSCRLNS=17 ;(see "BPS LSTMN LOG" LM template: Bottom=21, Top = 4, 21-4=17)
 N BPX,BPLN0,BPCNT,BPSTR1,BPSTYP2
 S BPLN0=BPLN
 S BPSTYP2=$S(BPSTYPE="C":"CLAIM REQUEST",BPSTYPE="R":"REVERSAL",1:"")
 S BPSTR1="Transmission Information ("_BPSTYP2_")(#"_BPIEN02_")"
 D SETLINE(.BPLN,BPSTR1_$$LINE^BPSSCRU3(79-$L(BPSTR1),"-"))
 D SETLINE(.BPLN,"Created on: "_$$CREATEDT(BPIEN02,BPSDTALT))
 D SETLINE(.BPLN,"VA Claim ID: "_$P($G(^BPSC(+BPIEN02,0)),U,1))
 D SETLINE(.BPLN,"Submitted By: "_$$SUBMTBY(BP57))
 D SETLINE(.BPLN,"Transaction Type: "_$$TRTYPE^BPSSCRU5($$TRCODE(BPIEN02)))
 D SETLINE(.BPLN,"Date of Service: "_$$DOSCLM(BPIEN02))
 D SETLINE(.BPLN,"NDC Code: "_$$LNDC^BPSSCRU5(BPIEN02))
 D SETLINE(.BPLN,"NCPDP Qty: "_$$QTY(BPIEN02)_" "_$$UNITS(BPIEN02))
 D SETLINE(.BPLN,"Days Supply: "_$$DAYSSUPL(BPIEN02))
 D SETLINE(.BPLN,"Division: "_$$DIV(BP57))
 D SETLINE(.BPLN,"NPI#: "_$$NPI(BPIEN02))
 D SETLINE(.BPLN,"ECME Pharmacy: "_$$DIVNAME^BPSSCRDS($$LDIV(BP57)))
 S BPX="Billed Qty: "_$$BILLQTY(BP57)_" "_$$BILLUNT(BP57)
 S BPX=BPX_"     Unit Cost: "_$$UNTPRICE(BP57)
 S BPX=BPX_"     Gross Amt Due: "_$$TOTPRICE(BPIEN02)
 D SETLINE(.BPLN,BPX)
 S BPX="Ingredient Cost: "_$$INGRCST(BPIEN02)
 S BPX=BPX_"     Dispensing Fee: "_$$DISPFEE(BPIEN02)
 D SETLINE(.BPLN,BPX)
 S BPX="U&C Charge: "_$$UCCHRG(BPIEN02)
 S BPX=BPX_"     Admin Fee: "_$$ADMNFEE(BPIEN02)
 D SETLINE(.BPLN,BPX)
 D SETLINE(.BPLN,"")
 D SETLINE(.BPLN,"Insurance Name: "_$$INSUR57(BP57))
 D SETLINE(.BPLN,"Group Name: "_$$GRPNM(BPIEN02))
 D SETLINE(.BPLN,"Rx Coordination of Benefits: "_$$RXCOB57(BP57))
 D SETLINE(.BPLN,"Pharmacy Plan ID: "_$$PHPLANID(BP57))
 D SETLINE(.BPLN,"BIN: "_$$BIN(BPIEN02))
 D SETLINE(.BPLN,"PCN: "_$$PCN(BPIEN02))
 D SETLINE(.BPLN,"NCPDP Version: "_$$GETVER(BPIEN02))
 D SETLINE(.BPLN,"Group ID: "_$$GRPID(BPIEN02))
 D SETLINE(.BPLN,"Cardholder ID: "_$$CRDHLDID(BPIEN02))
 D SETLINE(.BPLN,"Patient Relationship Code: "_$$PATRELSH(BPIEN02))
 D SETLINE(.BPLN,"Cardholder First Name: "_$$CRDHLDFN(BPIEN02,BP57))
 D SETLINE(.BPLN,"Cardholder Last Name: "_$$CRDHLDLN(BPIEN02,BP57))
 F BPCNT=BPLN:1:BPLN0+BPSCRLNS D SETLINE(.BPLN,"")
 S BPLN0=BPLN
 D SETLINE(.BPLN,"Billing Request Payer Sheet: "_$$B1PYRIEN^BPSSCRU5(BP57))
 D SETLINE(.BPLN,"Reversal Payer Sheet: "_$$B2PYRIEN^BPSSCRU5(BP57))
 D SETLINE(.BPLN,"")
 Q
 ;Submitted By User
SUBMTBY(BP57) ;
 N BPIEN,BPUSR
 S BPIEN=$P($G(^BPSTL(BP57,0)),U,10)
 S BPUSR=$$GETUSRNM^BPSSCRU1(BPIEN)
 Q $S(BPUSR']"":"UNKNOWN",1:BPUSR)
 ;Date of service
DOSCLM(BPIEN02) ;
 N BPDT
 S BPDT=$P($G(^BPSC(BPIEN02,401)),U,1)\1
 Q $E(BPDT,5,6)_"/"_$E(BPDT,7,8)_"/"_$E(BPDT,1,4)
 ;Create date 
CREATEDT(BPIEN02,BPSDTALT) ;
 N BPSDT
 S BPSDT=+$P($G(^BPSC(BPIEN02,0)),U,6)
 Q $$DATETIME^BPSSCRU5($S(BPSDT>0:BPSDT,1:BPSDTALT))
 ;Plan ID
PLANID(BP57) ;
 Q $P($G(^BPSTL(BP57,10,+$G(^BPSTL(BP57,9)),0)),U,1)
CERTMOD(BP57) ;
 Q $P($G(^BPSTL(BP57,10,+$G(^BPSTL(BP57,9)),0)),U,5)
 ;Software Vendor/Cert ID
CERTIEN(BP57) ;
 Q $P($G(^BPSTL(BP57,10,+$G(^BPSTL(BP57,9)),0)),U,6)
 ;Division
DIV(BP57) ;
 Q $$GET1^DIQ(9002313.57,BP57_",",11)
 ;NPI
NPI(BPIEN02) ;
 Q $$GET1^DIQ(9002313.02,BPIEN02_",",201)
 ;group ID
GRPID(BPIEN02) ;
 Q $E($P($G(^BPSC(BPIEN02,300)),U,1),3,99)
 ;Group Name
GRPNM(BPSIEN02) ;
 N BPSGPN
 S BPSGPN=$P($G(^BPSTL(BP57,10,+$G(^BPSTL(BP57,9)),3)),U,1)
 Q BPSGPN
 ;Cardholder ID
CRDHLDID(BPIEN02) ;
 Q $E($P($G(^BPSC(BPIEN02,300)),U,2),3,99)
 ;Cardholder First name
CRDHLDFN(BPIEN02,BP57) ;
 N Y
 S Y=$E($P($G(^BPSC(BPIEN02,300)),U,12),3,99)
 I $L(Y)=0 S Y=$P($G(^BPSTL(BP57,10,+$G(^BPSTL(BP57,9)),1)),U,6)
 Q Y
 ;Cardholder Last Name
CRDHLDLN(BPIEN02,BP57) ;
 N Y
 S Y=$E($P($G(^BPSC(BPIEN02,300)),U,13),3,99)
 I $L(Y)=0 S Y=$P($G(^BPSTL(BP57,10,+$G(^BPSTL(BP57,9)),1)),U,7)
 Q Y
 ;Patient Relationship Code
PATRELSH(BPIEN02) ;
 N Y
 S Y=$E($P($G(^BPSC(BPIEN02,300)),U,6),3,99)
 Q $S(Y=0:"NOT SPECIFIED",Y=1:"CARDHOLDER",Y=2:"SPOUSE",Y=3:"CHILD",Y=4:"OTHER",1:Y)
PCN(BPIEN02) ;
 Q $P($G(^BPSC(BPIEN02,100)),U,4)
 ; Get the Payer Sheet Version Number.
GETVER(BPIEN02) ;
 N BPSVER
 S BPSVER=$P($G(^BPSC(BPIEN02,100)),U,2)
 I $G(BPSVER)]"" S BPSVER=$E(BPSVER,1)_"."_$E(BPSVER,2,99)
 Q BPSVER
BIN(BPIEN02) ;
 Q $P($G(^BPSC(BPIEN02,100)),U,1)
 ;insurance name by 9002313.57 pointer
INSUR57(BPIEN57) ;
 N BPINSN
 S BPINSN=+$G(^BPSTL(BPIEN57,9))
 Q $P($G(^BPSTL(BPIEN57,10,BPINSN,0)),U,7)
 ;
PHPLANID(BPIEN57) ; Get the Pharmacy Plan ID from the BPS Log of Transactions file
 ; Input - BPSIEN57: IEN from the BPS Log of Transactions file.
 I '$G(BPIEN57) Q ""
 N BPINSN
 S BPINSN=+$G(^BPSTL(BPIEN57,9))
 Q $P($G(^BPSTL(BPIEN57,10,BPINSN,3)),U,3)
 ;
QTY(BPIEN02) ;
 Q $E($P($G(^BPSC(BPIEN02,400,1,440)),U,2),3,99)/1000
 ;NCPDP Units
UNITS(BPIEN02) ;
 I $G(BPIEN02)="" Q "(  )"
 N X
 S X=$E($P($G(^BPSC(BPIEN02,400,1,600)),U,1),3,99)
 Q $S(X="":"(  )",1:"("_X_")")
UNTPRICE(BPIEN57) ;
 I $G(BPIEN57)="" Q ""
 Q +$P($G(^BPSTL(BPIEN57,5)),U,2)
TOTPRICE(BPIEN02) ;
 I $G(BPIEN02)="" Q ""
 N X
 S X=$E($P($G(^BPSC(BPIEN02,400,1,400)),U,30),3,99)
 Q $S(X="":X,1:$$DFF2EXT^BPSECFM(X))
BILLQTY(BPIEN57) ;
 Q $P($G(^BPSTL(BPIEN57,5)),U,9)
BILLUNT(BPIEN57) ;
 I $G(BPIEN57)="" Q "(  )"
 N X
 S X=$P($G(^BPSTL(BPIEN57,5)),U,10)
 Q $S(X="":"(  )",1:"("_X_")")
 ;Ingredient Cost
INGRCST(BPIEN02) ;
 I $G(BPIEN02)="" Q ""
 N X
 S X=$E($P($G(^BPSC(BPIEN02,400,1,400)),U,9),3,99)
 Q $S(X="":X,1:$$DFF2EXT^BPSECFM(X))
 ;Dispensing Fee Submitted
DISPFEE(BPIEN02) ;
 I $G(BPIEN02)="" Q ""
 N X
 S X=$E($P($G(^BPSC(BPIEN02,400,1,400)),U,12),3,99)
 Q $S(X="":X,1:$$DFF2EXT^BPSECFM(X))
 ;U&C Charge
UCCHRG(BPIEN02) ;
 I $G(BPIEN02)="" Q ""
 N X
 S X=$E($P($G(^BPSC(BPIEN02,400,1,400)),U,26),3,99)
 Q $S(X="":X,1:$$DFF2EXT^BPSECFM(X))
 ;Admin Fee
ADMNFEE(BPIEN02) ;
 I $G(BPIEN02)="" Q ""
 N CNT,X,AF
 S AF="",CNT=0 F  S CNT=$O(^BPSC(BPIEN02,400,1,478.01,CNT)) Q:'CNT  D
 . S X=$G(^BPSC(BPIEN02,400,1,478.01,CNT,0))
 . I +$E($P(X,U,2),3,4)=4 S AF=AF+$$DFF2EXT^BPSECFM($E($P(X,U,3),3,10))
 Q $S(AF="":AF,1:$J(AF,0,2))
 ;get ECME pharmacy division ptr for LOG
LDIV(BPIEN57) ;
 Q +$P($G(^BPSTL(BPIEN57,1)),U,7)
 ;transaction code
TRCODE(BPIEN02) ;
 Q $P($G(^BPSC(BPIEN02,100)),U,3)
 ;days supply
DAYSSUPL(BPIEN02) ;
 ;format D5NNN -> NNN
 Q +$E($P($G(^BPSC(BPIEN02,400,1,400)),U,5),3,99)
 ;
 ;display response record
DISPRSP(BPLN,BP59,BPIEN03,BP57,BPSTYPE,BPSDTALT) ;
 N BPSCRLNS S BPSCRLNS=17 ;(see "BPS LSTMN LOG" LM template: Bottom=21, Top = 4, 21-4=17)
 N BPX,BPLN0,BPCNT,BPRJCDS,BPRJ,BPSTR1,BPSTYP2,BDUR,BMSG,PTRESP
 S BPLN0=BPLN
 S BPSTYP2=$S(BPSTYPE="C":"CLAIM REQUEST",BPSTYPE="R":"REVERSAL",1:"")
 S BPSTR1="Response Information  ("_BPSTYP2_")(#"_BPIEN03_")"
 D SETLINE(.BPLN,BPSTR1_$$LINE^BPSSCRU3(79-$L(BPSTR1),"-"))
 D SETLINE(.BPLN,"Response Received: "_$$RESPREC(BPIEN03,BPSDTALT))
 D SETLINE(.BPLN,"Date of Service: "_$$DOSRSP(BPIEN03))
 D SETLINE(.BPLN,"Transaction Response Status: "_$$RESPSTAT^BPSSCRU5(BPIEN03))
 D SETLINE(.BPLN,"Total Amount Paid: $"_$$TOTAMNT(BPIEN03))
 D SETLINE(.BPLN,"Ingredient Cost Paid: $"_$$ICPAID(BPIEN03)_"   Dispensing Fee Paid: $"_$$DFPAID(BPIEN03))
 S PTRESP=$$PTRESP(BPIEN03) S PTRESP=$S(PTRESP="":"$",PTRESP="0.00":"$0",1:"($"_PTRESP_")")
 D SETLINE(.BPLN,"Patient Resp (INS): "_PTRESP)
 D SETLINE(.BPLN,"Reject code(s): ")
 D REJCODES^BPSSCRU5(BPIEN03,.BPRJCDS)
 S BPRJ=""
 F  S BPRJ=$O(BPRJCDS(BPRJ)) Q:BPRJ=""  D
 . D SETLINE(.BPLN," "_$$GETRJNAM^BPSSCRU3(BPRJ))
 D WRAPLN^BPSSCRU5(.BPLN,$$MESSAGE(BPIEN03),76,"Payer Message: ",5)
 D ADDMESS(BPIEN03,1,.BPADDMSG)
 S BMSG="" F  S BMSG=$O(BPADDMSG(BMSG)) Q:BMSG=""  D
 . D WRAPLN^BPSSCRU5(.BPLN,BPADDMSG(BMSG),76,$S(BMSG=1:"Payer Additional Message: ",1:"     "),5)
 D SETLINE(.BPLN,"Reason for Service Code: "_$$DURREAS(BPIEN03))
 D SETLINE(.BPLN,"DUR Text: "_$$DURTEXT(BPIEN03))
 D WRAPLN^BPSSCRU5(.BPLN,$$DURADD(BPIEN03),76,"DUR Additional Text: ",5)
 F BPCNT=1:1:2 D SETLINE(.BPLN,"")
 Q
 ;
RESPREC(BPIEN03,BPSDTALT) ;
 N BPSDT
 S BPSDT=+$P($G(^BPSR(BPIEN03,0)),U,2)
 Q $$DATETIME^BPSSCRU5($S(BPSDT>0:BPSDT,1:BPSDTALT))
 ;
DOSRSP(BPIEN03) ;
 N BPDT
 S BPDT=$P($G(^BPSR(BPIEN03,400)),U,1)\1
 Q $E(BPDT,5,6)_"/"_$E(BPDT,7,8)_"/"_$E(BPDT,1,4)
 ;
TOTAMNT(BPIEN03) ;
 I $G(BPIEN03)="" Q ""
 N X
 S X=$P($G(^BPSR(BPIEN03,1000,1,500)),U,9)
 Q $S(X="":X,1:$$DFF2EXT^BPSECFM(X))
 ;
ICPAID(BPIEN03) ;Ingredient Cost Paid
 I $G(BPIEN03)="" Q ""
 N X
 S X=$P($G(^BPSR(BPIEN03,1000,1,500)),U,6)
 Q $S(X="":X,1:$$DFF2EXT^BPSECFM(X))
 ;
DFPAID(BPIEN03) ;Dispensing Fee Paid
 I $G(BPIEN03)="" Q ""
 N X
 S X=$P($G(^BPSR(BPIEN03,1000,1,500)),U,7)
 Q $S(X="":X,1:$$DFF2EXT^BPSECFM(X))
 ;
PTRESP(BPIEN03) ;Patient Responsibility
 I $G(BPIEN03)="" Q ""
 N X
 S X=$P($G(^BPSR(BPIEN03,1000,1,500)),U,5)
 Q $S(X="":X,1:$$DFF2EXT^BPSECFM(X))
 ;
MESSAGE(BPIEN03) ;
 Q $P($G(^BPSR(BPIEN03,504)),U)
 ;
ADDMESS(BPIEN03,POS,BPADDMSG) ;
 N ADM,X,QUA,TXT,CON,BPMTMP,L,NEXT
 K BPMTMP,BPADDMSG
 I '$G(BPIEN03) Q
 I '$G(POS) S POS=1
 S (ADM,L)=0 F  S ADM=$O(^BPSR(BPIEN03,1000,POS,130.01,ADM)) Q:'ADM  D
 . S X=$G(^BPSR(BPIEN03,1000,POS,130.01,ADM,0))
 . S TXT=$P($G(^BPSR(BPIEN03,1000,POS,130.01,ADM,1)),U,1)
 . S QUA=$P(X,U,3),CON=$P(X,U,2)
 . ; This should not happen, but if the qualifier is null, set it 
 . ;  to "Z"_concatenated with a unique number so that it follows the
 . ;  other qualifiers.  Per the D0 standard, qualifiers can be 1-9 and
 . ;  A-Z.  ECL limits this to 1-9 but an future ECL may extend this.
 . I QUA="" S L=L+1,QUA="Z"_L
 . S BPMTMP(QUA)=CON_U_TXT
 I '$D(BPMTMP) Q
 S L=0,(QUA,NEXT)="" F  S QUA=$O(BPMTMP(QUA)) Q:QUA=""  D
 . S CON=$P(BPMTMP(QUA),U,1),TXT=$P(BPMTMP(QUA),U,2)
 . I NEXT="+" S BPADDMSG(L)=BPADDMSG(L)_TXT,NEXT=CON Q
 . S L=L+1,BPADDMSG(L)=TXT,NEXT=CON
 Q
 ;
DURTEXT(BPIEN03) ;
 ; DUR FREE TEXT MESSAGE from first instance of DUR PPS RESPONSE
 Q $P($G(^BPSR(BPIEN03,1000,1,567.01,1,0)),U,9)
 ;
DURREAS(BPIEN03) ;
 ; REASON FOR SERVICE CODE from first instance of DUR PPS RESPONSE
 Q $$GET1^DIQ(9002313.1101,"1,1,"_BPIEN03_",",439)
 ;
DURADD(BPIEN03) ;
 ; DUR ADDITIONAL TEXT from first instance of DUR PPS RESPONSE
 Q $P($G(^BPSR(BPIEN03,1000,1,567.01,1,1)),U)
 ;
RXCOB57(BPIEN57) ;
 N BPCOB
 S BPCOB=+$P($G(^BPSTL(BPIEN57,0)),U,14)
 Q $S(BPCOB=2:"SECONDARY",BPCOB=3:"TERTIARY",1:"PRIMARY")
 ;
 ;Display other payer(s)
DISPPYR(BPLN,BPIEN03) ;
 N PYR,PYRDATA,BPSTR1
 S PYR=0 F  S PYR=$O(^BPSR(BPIEN03,1000,1,355.01,PYR)) Q:'PYR  D
 . S PYRDATA=^BPSR(BPIEN03,1000,1,355.01,PYR,1)
 . S BPSTR1="Other Payer Information ("_PYR_")(#"_BPIEN03_")"
 . D SETLINE(.BPLN,BPSTR1_$$LINE^BPSSCRU3(79-$L(BPSTR1),"-"))
 . D SETLINE(.BPLN,"Other Payer ID Count: "_$$PYRIDCNT(BPIEN03,PYR))
 . D SETLINE(.BPLN,"Other Payer ID: "_$P(PYRDATA,U,3))
 . D SETLINE(.BPLN,"Other Payer Coverage Type: "_$P(PYRDATA,U,1))
 . D SETLINE(.BPLN,"Other Payer ID Qualifier: "_$P(PYRDATA,U,2))
 . D SETLINE(.BPLN,"Other Payer Help Desk Phone Number: "_$P(PYRDATA,U,8))
 . D SETLINE(.BPLN,"Other Payer Processor Control Number: "_$P(PYRDATA,U,4))
 . D SETLINE(.BPLN,"Other Payer Effective Date: "_$P(PYRDATA,U,10))
 . D SETLINE(.BPLN,"Other Payer Termination Date: "_$P(PYRDATA,U,11))
 . D SETLINE(.BPLN,"Other Payer Person Code: "_$P(PYRDATA,U,7))
 . D SETLINE(.BPLN,"Other Payer Patient Relationship Code: "_$P(PYRDATA,U,9))
 . D SETLINE(.BPLN,"Other Payer Cardholder ID: "_$P(PYRDATA,U,5))
 . D SETLINE(.BPLN,"Other Payer Group ID: "_$P(PYRDATA,U,6))
 Q
 ;
PYRIDCNT(BPIEN03,PYR) ;
 Q $P($G(^BPSR(BPIEN03,1000,1,355.01,PYR,0)),U)
